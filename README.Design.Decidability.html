<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Design.Decidability</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Examples of decision procedures and how to use them</a>
<a id="161" class="Comment">------------------------------------------------------------------------</a>

<a id="235" class="Symbol">{-#</a> <a id="239" class="Keyword">OPTIONS</a> <a id="247" class="Pragma">--cubical-compatible</a> <a id="268" class="Pragma">--safe</a> <a id="275" class="Symbol">#-}</a>

<a id="280" class="Keyword">module</a> <a id="287" href="README.Design.Decidability.html" class="Module">README.Design.Decidability</a> <a id="314" class="Keyword">where</a>

<a id="321" class="Keyword">open</a> <a id="326" class="Keyword">import</a> <a id="333" href="Data.Bool.html" class="Module">Data.Bool</a>
<a id="343" class="Keyword">open</a> <a id="348" class="Keyword">import</a> <a id="355" href="Data.List.html" class="Module">Data.List</a>
<a id="365" class="Keyword">open</a> <a id="370" class="Keyword">import</a> <a id="377" href="Data.List.Properties.html" class="Module">Data.List.Properties</a> <a id="398" class="Keyword">using</a> <a id="404" class="Symbol">(</a><a id="405" href="Data.List.Properties.html#2220" class="Function">∷-injective</a><a id="416" class="Symbol">)</a>
<a id="418" class="Keyword">open</a> <a id="423" class="Keyword">import</a> <a id="430" href="Data.Nat.html" class="Module">Data.Nat</a>
<a id="439" class="Keyword">open</a> <a id="444" class="Keyword">import</a> <a id="451" href="Data.Nat.Properties.html" class="Module">Data.Nat.Properties</a> <a id="471" class="Keyword">using</a> <a id="477" class="Symbol">(</a><a id="478" href="Data.Nat.Properties.html#2327" class="Function">suc-injective</a><a id="491" class="Symbol">)</a>
<a id="493" class="Keyword">open</a> <a id="498" class="Keyword">import</a> <a id="505" href="Data.Product.html" class="Module">Data.Product</a>
<a id="518" class="Keyword">open</a> <a id="523" class="Keyword">import</a> <a id="530" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="540" class="Keyword">open</a> <a id="545" class="Keyword">import</a> <a id="552" href="Function.html" class="Module">Function</a>
<a id="561" class="Keyword">open</a> <a id="566" class="Keyword">import</a> <a id="573" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>
<a id="611" class="Keyword">open</a> <a id="616" class="Keyword">import</a> <a id="623" href="Relation.Nary.html" class="Module">Relation.Nary</a>

<a id="638" class="Comment">------------------------------------------------------------------------</a>
<a id="711" class="Comment">-- Reflects</a>

<a id="724" class="Keyword">open</a> <a id="729" class="Keyword">import</a> <a id="736" href="Relation.Nullary.Reflects.html" class="Module">Relation.Nullary.Reflects</a>

<a id="763" class="Keyword">infix</a> <a id="769" class="Number">4</a> <a id="771" href="README.Design.Decidability.html#1906" class="Function Operator">_≟₀_</a> <a id="776" href="README.Design.Decidability.html#2534" class="Function Operator">_≟₁_</a> <a id="781" href="README.Design.Decidability.html#3123" class="Function Operator">_≟₂_</a>

<a id="787" class="Comment">-- A proof of `Reflects P b` shows that a proposition `P` has the truth value of</a>
<a id="868" class="Comment">-- the boolean `b`. A proof of `Reflects P true` amounts to a proof of `P`, and</a>
<a id="948" class="Comment">-- a proof of `Reflects P false` amounts to a refutation of `P`.</a>

<a id="ex₀"></a><a id="1014" href="README.Design.Decidability.html#1014" class="Function">ex₀</a> <a id="1018" class="Symbol">:</a> <a id="1020" class="Symbol">(</a><a id="1021" href="README.Design.Decidability.html#1021" class="Bound">n</a> <a id="1023" class="Symbol">:</a> <a id="1025" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="1026" class="Symbol">)</a> <a id="1028" class="Symbol">→</a> <a id="1030" href="Relation.Nullary.Reflects.html#899" class="Datatype">Reflects</a> <a id="1039" class="Symbol">(</a><a id="1040" href="README.Design.Decidability.html#1021" class="Bound">n</a> <a id="1042" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1044" href="README.Design.Decidability.html#1021" class="Bound">n</a><a id="1045" class="Symbol">)</a> <a id="1047" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a>
<a id="1052" href="README.Design.Decidability.html#1014" class="Function">ex₀</a> <a id="1056" href="README.Design.Decidability.html#1056" class="Bound">n</a> <a id="1058" class="Symbol">=</a> <a id="1060" href="Relation.Nullary.Reflects.html#947" class="InductiveConstructor">ofʸ</a> <a id="1064" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="ex₁"></a><a id="1070" href="README.Design.Decidability.html#1070" class="Function">ex₁</a> <a id="1074" class="Symbol">:</a> <a id="1076" class="Symbol">(</a><a id="1077" href="README.Design.Decidability.html#1077" class="Bound">n</a> <a id="1079" class="Symbol">:</a> <a id="1081" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="1082" class="Symbol">)</a> <a id="1084" class="Symbol">→</a> <a id="1086" href="Relation.Nullary.Reflects.html#899" class="Datatype">Reflects</a> <a id="1095" class="Symbol">(</a><a id="1096" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a> <a id="1101" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1103" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1107" href="README.Design.Decidability.html#1077" class="Bound">n</a><a id="1108" class="Symbol">)</a> <a id="1110" href="Agda.Builtin.Bool.html#175" class="InductiveConstructor">false</a>
<a id="1116" href="README.Design.Decidability.html#1070" class="Function">ex₁</a> <a id="1120" href="README.Design.Decidability.html#1120" class="Bound">n</a> <a id="1122" class="Symbol">=</a> <a id="1124" href="Relation.Nullary.Reflects.html#984" class="InductiveConstructor">ofⁿ</a> <a id="1128" class="Symbol">λ</a> <a id="1130" class="Symbol">()</a>

<a id="ex₂"></a><a id="1134" href="README.Design.Decidability.html#1134" class="Function">ex₂</a> <a id="1138" class="Symbol">:</a> <a id="1140" class="Symbol">(</a><a id="1141" href="README.Design.Decidability.html#1141" class="Bound">b</a> <a id="1143" class="Symbol">:</a> <a id="1145" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a><a id="1149" class="Symbol">)</a> <a id="1151" class="Symbol">→</a> <a id="1153" href="Relation.Nullary.Reflects.html#899" class="Datatype">Reflects</a> <a id="1162" class="Symbol">(</a><a id="1163" href="Data.Bool.Base.html#1460" class="Function">T</a> <a id="1165" href="README.Design.Decidability.html#1141" class="Bound">b</a><a id="1166" class="Symbol">)</a> <a id="1168" href="README.Design.Decidability.html#1141" class="Bound">b</a>
<a id="1170" href="README.Design.Decidability.html#1134" class="Function">ex₂</a> <a id="1174" href="Agda.Builtin.Bool.html#175" class="InductiveConstructor">false</a> <a id="1180" class="Symbol">=</a> <a id="1182" href="Relation.Nullary.Reflects.html#984" class="InductiveConstructor">ofⁿ</a> <a id="1186" href="Function.Base.html#704" class="Function">id</a>
<a id="1189" href="README.Design.Decidability.html#1134" class="Function">ex₂</a> <a id="1193" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a>  <a id="1199" class="Symbol">=</a> <a id="1201" href="Relation.Nullary.Reflects.html#947" class="InductiveConstructor">ofʸ</a> <a id="1205" href="Agda.Builtin.Unit.html#195" class="InductiveConstructor">tt</a>

<a id="1209" class="Comment">------------------------------------------------------------------------</a>
<a id="1282" class="Comment">-- Dec</a>

<a id="1290" class="Keyword">open</a> <a id="1295" class="Keyword">import</a> <a id="1302" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a>

<a id="1330" class="Comment">-- A proof of `Dec P` is a proof of `Reflects P b` for some `b`.</a>
<a id="1395" class="Comment">-- `Dec P` is declared as a record, with fields:</a>
<a id="1444" class="Comment">--   does : Bool</a>
<a id="1461" class="Comment">--   proof : Reflects P does</a>

<a id="ex₃"></a><a id="1491" href="README.Design.Decidability.html#1491" class="Function">ex₃</a> <a id="1495" class="Symbol">:</a> <a id="1497" class="Symbol">(</a><a id="1498" href="README.Design.Decidability.html#1498" class="Bound">b</a> <a id="1500" class="Symbol">:</a> <a id="1502" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a><a id="1506" class="Symbol">)</a> <a id="1508" class="Symbol">→</a> <a id="1510" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="1514" class="Symbol">(</a><a id="1515" href="Data.Bool.Base.html#1460" class="Function">T</a> <a id="1517" href="README.Design.Decidability.html#1498" class="Bound">b</a><a id="1518" class="Symbol">)</a>
<a id="1520" href="Relation.Nullary.Decidable.Core.html#1555" class="Field">does</a>  <a id="1526" class="Symbol">(</a><a id="1527" href="README.Design.Decidability.html#1491" class="Function">ex₃</a> <a id="1531" href="README.Design.Decidability.html#1531" class="Bound">b</a><a id="1532" class="Symbol">)</a> <a id="1534" class="Symbol">=</a> <a id="1536" href="README.Design.Decidability.html#1531" class="Bound">b</a>
<a id="1538" href="Relation.Nullary.Decidable.Core.html#1572" class="Field">proof</a> <a id="1544" class="Symbol">(</a><a id="1545" href="README.Design.Decidability.html#1491" class="Function">ex₃</a> <a id="1549" href="README.Design.Decidability.html#1549" class="Bound">b</a><a id="1550" class="Symbol">)</a> <a id="1552" class="Symbol">=</a> <a id="1554" href="README.Design.Decidability.html#1134" class="Function">ex₂</a> <a id="1558" href="README.Design.Decidability.html#1549" class="Bound">b</a>

<a id="1561" class="Comment">-- We also have pattern synonyms `yes` and `no`, allowing both fields to be</a>
<a id="1637" class="Comment">-- given at once.</a>

<a id="ex₄"></a><a id="1656" href="README.Design.Decidability.html#1656" class="Function">ex₄</a> <a id="1660" class="Symbol">:</a> <a id="1662" class="Symbol">(</a><a id="1663" href="README.Design.Decidability.html#1663" class="Bound">n</a> <a id="1665" class="Symbol">:</a> <a id="1667" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="1668" class="Symbol">)</a> <a id="1670" class="Symbol">→</a> <a id="1672" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="1676" class="Symbol">(</a><a id="1677" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a> <a id="1682" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1684" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1688" href="README.Design.Decidability.html#1663" class="Bound">n</a><a id="1689" class="Symbol">)</a>
<a id="1691" href="README.Design.Decidability.html#1656" class="Function">ex₄</a> <a id="1695" href="README.Design.Decidability.html#1695" class="Bound">n</a> <a id="1697" class="Symbol">=</a> <a id="1699" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="1702" class="Symbol">λ</a> <a id="1704" class="Symbol">()</a>

<a id="1708" class="Comment">-- It is possible, but not ideal, to define recursive decision procedures using</a>
<a id="1788" class="Comment">-- only the `yes` and `no` patterns. The following procedure decides whether two</a>
<a id="1869" class="Comment">-- given natural numbers are equal.</a>

<a id="_≟₀_"></a><a id="1906" href="README.Design.Decidability.html#1906" class="Function Operator">_≟₀_</a> <a id="1911" class="Symbol">:</a> <a id="1913" class="Symbol">(</a><a id="1914" href="README.Design.Decidability.html#1914" class="Bound">m</a> <a id="1916" href="README.Design.Decidability.html#1916" class="Bound">n</a> <a id="1918" class="Symbol">:</a> <a id="1920" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="1921" class="Symbol">)</a> <a id="1923" class="Symbol">→</a> <a id="1925" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="1929" class="Symbol">(</a><a id="1930" href="README.Design.Decidability.html#1914" class="Bound">m</a> <a id="1932" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1934" href="README.Design.Decidability.html#1916" class="Bound">n</a><a id="1935" class="Symbol">)</a>
<a id="1937" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="1943" href="README.Design.Decidability.html#1906" class="Function Operator">≟₀</a> <a id="1946" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="1952" class="Symbol">=</a> <a id="1954" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="1958" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
<a id="1963" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="1969" href="README.Design.Decidability.html#1906" class="Function Operator">≟₀</a> <a id="1972" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1976" href="README.Design.Decidability.html#1976" class="Bound">n</a> <a id="1978" class="Symbol">=</a> <a id="1980" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="1983" class="Symbol">λ</a> <a id="1985" class="Symbol">()</a>
<a id="1988" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1992" href="README.Design.Decidability.html#1992" class="Bound">m</a> <a id="1994" href="README.Design.Decidability.html#1906" class="Function Operator">≟₀</a> <a id="1997" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="2003" class="Symbol">=</a> <a id="2005" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2008" class="Symbol">λ</a> <a id="2010" class="Symbol">()</a>
<a id="2013" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2017" href="README.Design.Decidability.html#2017" class="Bound">m</a> <a id="2019" href="README.Design.Decidability.html#1906" class="Function Operator">≟₀</a> <a id="2022" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2026" href="README.Design.Decidability.html#2026" class="Bound">n</a> <a id="2028" class="Keyword">with</a> <a id="2033" href="README.Design.Decidability.html#2017" class="Bound">m</a> <a id="2035" href="README.Design.Decidability.html#1906" class="Function Operator">≟₀</a> <a id="2038" href="README.Design.Decidability.html#2026" class="Bound">n</a>
<a id="2040" class="Symbol">...</a> <a id="2044" class="Symbol">|</a> <a id="2046" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="2050" href="README.Design.Decidability.html#2050" class="Bound">p</a> <a id="2052" class="Symbol">=</a> <a id="2054" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="2058" class="Symbol">(</a><a id="2059" href="Relation.Binary.PropositionalEquality.Core.html#1153" class="Function">cong</a> <a id="2064" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2068" href="README.Design.Decidability.html#2050" class="Bound">p</a><a id="2069" class="Symbol">)</a>
<a id="2071" class="Symbol">...</a> <a id="2075" class="Symbol">|</a> <a id="2077" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2080" href="README.Design.Decidability.html#2080" class="Bound">¬p</a> <a id="2083" class="Symbol">=</a> <a id="2085" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2088" class="Symbol">(</a><a id="2089" href="README.Design.Decidability.html#2080" class="Bound">¬p</a> <a id="2092" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="2094" href="Data.Nat.Properties.html#2327" class="Function">suc-injective</a><a id="2107" class="Symbol">)</a>

<a id="2110" class="Comment">-- In this case, we can see that `does (suc m ≟ suc n)` should be equal to</a>
<a id="2185" class="Comment">-- `does (m ≟ n)`, because a `yes` from `m ≟ n` gives rise to a `yes` from the</a>
<a id="2264" class="Comment">-- result, and similarly for `no`. However, in the above definition, this</a>
<a id="2338" class="Comment">-- equality does not hold definitionally, because we always do a case split</a>
<a id="2414" class="Comment">-- before returning a result. To avoid this, we can return the `does` part</a>
<a id="2489" class="Comment">-- separately, before any pattern matching.</a>

<a id="_≟₁_"></a><a id="2534" href="README.Design.Decidability.html#2534" class="Function Operator">_≟₁_</a> <a id="2539" class="Symbol">:</a> <a id="2541" class="Symbol">(</a><a id="2542" href="README.Design.Decidability.html#2542" class="Bound">m</a> <a id="2544" href="README.Design.Decidability.html#2544" class="Bound">n</a> <a id="2546" class="Symbol">:</a> <a id="2548" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2549" class="Symbol">)</a> <a id="2551" class="Symbol">→</a> <a id="2553" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="2557" class="Symbol">(</a><a id="2558" href="README.Design.Decidability.html#2542" class="Bound">m</a> <a id="2560" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2562" href="README.Design.Decidability.html#2544" class="Bound">n</a><a id="2563" class="Symbol">)</a>
<a id="2565" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="2571" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2574" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="2580" class="Symbol">=</a> <a id="2582" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="2586" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
<a id="2591" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="2597" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2600" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2604" href="README.Design.Decidability.html#2604" class="Bound">n</a> <a id="2606" class="Symbol">=</a> <a id="2608" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2611" class="Symbol">λ</a> <a id="2613" class="Symbol">()</a>
<a id="2616" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2620" href="README.Design.Decidability.html#2620" class="Bound">m</a> <a id="2622" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2625" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="2631" class="Symbol">=</a> <a id="2633" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2636" class="Symbol">λ</a> <a id="2638" class="Symbol">()</a>
<a id="2641" href="Relation.Nullary.Decidable.Core.html#1555" class="Field">does</a>  <a id="2647" class="Symbol">(</a><a id="2648" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2652" href="README.Design.Decidability.html#2652" class="Bound">m</a> <a id="2654" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2657" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2661" href="README.Design.Decidability.html#2661" class="Bound">n</a><a id="2662" class="Symbol">)</a> <a id="2664" class="Symbol">=</a> <a id="2666" href="Relation.Nullary.Decidable.Core.html#1555" class="Field">does</a> <a id="2671" class="Symbol">(</a><a id="2672" href="README.Design.Decidability.html#2652" class="Bound">m</a> <a id="2674" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2677" href="README.Design.Decidability.html#2661" class="Bound">n</a><a id="2678" class="Symbol">)</a>
<a id="2680" href="Relation.Nullary.Decidable.Core.html#1572" class="Field">proof</a> <a id="2686" class="Symbol">(</a><a id="2687" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2691" href="README.Design.Decidability.html#2691" class="Bound">m</a> <a id="2693" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2696" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2700" href="README.Design.Decidability.html#2700" class="Bound">n</a><a id="2701" class="Symbol">)</a> <a id="2703" class="Keyword">with</a> <a id="2708" href="README.Design.Decidability.html#2691" class="Bound">m</a> <a id="2710" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2713" href="README.Design.Decidability.html#2700" class="Bound">n</a>
<a id="2715" class="Symbol">...</a> <a id="2719" class="Symbol">|</a> <a id="2721" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="2725" href="README.Design.Decidability.html#2725" class="Bound">p</a> <a id="2727" class="Symbol">=</a> <a id="2729" href="Relation.Nullary.Reflects.html#947" class="InductiveConstructor">ofʸ</a> <a id="2733" class="Symbol">(</a><a id="2734" href="Relation.Binary.PropositionalEquality.Core.html#1153" class="Function">cong</a> <a id="2739" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2743" href="README.Design.Decidability.html#2725" class="Bound">p</a><a id="2744" class="Symbol">)</a>
<a id="2746" class="Symbol">...</a> <a id="2750" class="Symbol">|</a> <a id="2752" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2755" href="README.Design.Decidability.html#2755" class="Bound">¬p</a> <a id="2758" class="Symbol">=</a> <a id="2760" href="Relation.Nullary.Reflects.html#984" class="InductiveConstructor">ofⁿ</a> <a id="2764" class="Symbol">(</a><a id="2765" href="README.Design.Decidability.html#2755" class="Bound">¬p</a> <a id="2768" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="2770" href="Data.Nat.Properties.html#2327" class="Function">suc-injective</a><a id="2783" class="Symbol">)</a>

<a id="2786" class="Comment">-- We now get definitional equalities such as the following.</a>

<a id="2848" href="README.Design.Decidability.html#2848" class="Function">_</a> <a id="2850" class="Symbol">:</a> <a id="2852" class="Symbol">(</a><a id="2853" href="README.Design.Decidability.html#2853" class="Bound">m</a> <a id="2855" href="README.Design.Decidability.html#2855" class="Bound">n</a> <a id="2857" class="Symbol">:</a> <a id="2859" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2860" class="Symbol">)</a> <a id="2862" class="Symbol">→</a> <a id="2864" href="Relation.Nullary.Decidable.Core.html#1555" class="Field">does</a> <a id="2869" class="Symbol">(</a><a id="2870" class="Number">5</a> <a id="2872" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2874" href="README.Design.Decidability.html#2853" class="Bound">m</a> <a id="2876" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2879" class="Number">3</a> <a id="2881" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2883" href="README.Design.Decidability.html#2855" class="Bound">n</a><a id="2884" class="Symbol">)</a> <a id="2886" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2888" href="Relation.Nullary.Decidable.Core.html#1555" class="Field">does</a> <a id="2893" class="Symbol">(</a><a id="2894" class="Number">2</a> <a id="2896" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="2898" href="README.Design.Decidability.html#2853" class="Bound">m</a> <a id="2900" href="README.Design.Decidability.html#2534" class="Function Operator">≟₁</a> <a id="2903" href="README.Design.Decidability.html#2855" class="Bound">n</a><a id="2904" class="Symbol">)</a>
<a id="2906" class="Symbol">_</a> <a id="2908" class="Symbol">=</a> <a id="2910" class="Symbol">λ</a> <a id="2912" href="README.Design.Decidability.html#2912" class="Bound">m</a> <a id="2914" href="README.Design.Decidability.html#2914" class="Bound">n</a> <a id="2916" class="Symbol">→</a> <a id="2918" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="2924" class="Comment">-- Even better, from a maintainability point of view, is to use `map` or `map′`,</a>
<a id="3005" class="Comment">-- both of which capture the pattern of the `does` field remaining the same, but</a>
<a id="3086" class="Comment">-- the `proof` field being updated.</a>

<a id="_≟₂_"></a><a id="3123" href="README.Design.Decidability.html#3123" class="Function Operator">_≟₂_</a> <a id="3128" class="Symbol">:</a> <a id="3130" class="Symbol">(</a><a id="3131" href="README.Design.Decidability.html#3131" class="Bound">m</a> <a id="3133" href="README.Design.Decidability.html#3133" class="Bound">n</a> <a id="3135" class="Symbol">:</a> <a id="3137" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="3138" class="Symbol">)</a> <a id="3140" class="Symbol">→</a> <a id="3142" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="3146" class="Symbol">(</a><a id="3147" href="README.Design.Decidability.html#3131" class="Bound">m</a> <a id="3149" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3151" href="README.Design.Decidability.html#3133" class="Bound">n</a><a id="3152" class="Symbol">)</a>
<a id="3154" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="3160" href="README.Design.Decidability.html#3123" class="Function Operator">≟₂</a> <a id="3163" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="3169" class="Symbol">=</a> <a id="3171" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="3175" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
<a id="3180" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="3186" href="README.Design.Decidability.html#3123" class="Function Operator">≟₂</a> <a id="3189" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="3193" href="README.Design.Decidability.html#3193" class="Bound">n</a> <a id="3195" class="Symbol">=</a> <a id="3197" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="3200" class="Symbol">λ</a> <a id="3202" class="Symbol">()</a>
<a id="3205" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="3209" href="README.Design.Decidability.html#3209" class="Bound">m</a> <a id="3211" href="README.Design.Decidability.html#3123" class="Function Operator">≟₂</a> <a id="3214" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>  <a id="3220" class="Symbol">=</a> <a id="3222" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="3225" class="Symbol">λ</a> <a id="3227" class="Symbol">()</a>
<a id="3230" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="3234" href="README.Design.Decidability.html#3234" class="Bound">m</a> <a id="3236" href="README.Design.Decidability.html#3123" class="Function Operator">≟₂</a> <a id="3239" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="3243" href="README.Design.Decidability.html#3243" class="Bound">n</a> <a id="3245" class="Symbol">=</a> <a id="3247" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="3252" class="Symbol">(</a><a id="3253" href="Relation.Binary.PropositionalEquality.Core.html#1153" class="Function">cong</a> <a id="3258" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a><a id="3261" class="Symbol">)</a> <a id="3263" href="Data.Nat.Properties.html#2327" class="Function">suc-injective</a> <a id="3277" class="Symbol">(</a><a id="3278" href="README.Design.Decidability.html#3234" class="Bound">m</a> <a id="3280" href="README.Design.Decidability.html#3123" class="Function Operator">≟₂</a> <a id="3283" href="README.Design.Decidability.html#3243" class="Bound">n</a><a id="3284" class="Symbol">)</a>

<a id="3287" href="README.Design.Decidability.html#3287" class="Function">_</a> <a id="3289" class="Symbol">:</a> <a id="3291" class="Symbol">(</a><a id="3292" href="README.Design.Decidability.html#3292" class="Bound">m</a> <a id="3294" href="README.Design.Decidability.html#3294" class="Bound">n</a> <a id="3296" class="Symbol">:</a> <a id="3298" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="3299" class="Symbol">)</a> <a id="3301" class="Symbol">→</a> <a id="3303" href="Relation.Nullary.Decidable.Core.html#1555" class="Field">does</a> <a id="3308" class="Symbol">(</a><a id="3309" class="Number">5</a> <a id="3311" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3313" href="README.Design.Decidability.html#3292" class="Bound">m</a> <a id="3315" href="README.Design.Decidability.html#3123" class="Function Operator">≟₂</a> <a id="3318" class="Number">3</a> <a id="3320" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3322" href="README.Design.Decidability.html#3294" class="Bound">n</a><a id="3323" class="Symbol">)</a> <a id="3325" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3327" href="Relation.Nullary.Decidable.Core.html#1555" class="Field">does</a> <a id="3332" class="Symbol">(</a><a id="3333" class="Number">2</a> <a id="3335" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3337" href="README.Design.Decidability.html#3292" class="Bound">m</a> <a id="3339" href="README.Design.Decidability.html#3123" class="Function Operator">≟₂</a> <a id="3342" href="README.Design.Decidability.html#3294" class="Bound">n</a><a id="3343" class="Symbol">)</a>
<a id="3345" class="Symbol">_</a> <a id="3347" class="Symbol">=</a> <a id="3349" class="Symbol">λ</a> <a id="3351" href="README.Design.Decidability.html#3351" class="Bound">m</a> <a id="3353" href="README.Design.Decidability.html#3353" class="Bound">n</a> <a id="3355" class="Symbol">→</a> <a id="3357" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="3363" class="Comment">-- `map′` can be used in conjunction with combinators such as `_⊎-dec_` and</a>
<a id="3439" class="Comment">-- `_×-dec_` to build complex (simply typed) decision procedures.</a>

<a id="3506" class="Keyword">module</a> <a id="ListDecEq₀"></a><a id="3513" href="README.Design.Decidability.html#3513" class="Module">ListDecEq₀</a> <a id="3524" class="Symbol">{</a><a id="3525" href="README.Design.Decidability.html#3525" class="Bound">a</a><a id="3526" class="Symbol">}</a> <a id="3528" class="Symbol">{</a><a id="3529" href="README.Design.Decidability.html#3529" class="Bound">A</a> <a id="3531" class="Symbol">:</a> <a id="3533" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="3537" href="README.Design.Decidability.html#3525" class="Bound">a</a><a id="3538" class="Symbol">}</a> <a id="3540" class="Symbol">(</a><a id="3541" href="README.Design.Decidability.html#3541" class="Bound Operator">_≟ᴬ_</a> <a id="3546" class="Symbol">:</a> <a id="3548" class="Symbol">(</a><a id="3549" href="README.Design.Decidability.html#3549" class="Bound">x</a> <a id="3551" href="README.Design.Decidability.html#3551" class="Bound">y</a> <a id="3553" class="Symbol">:</a> <a id="3555" href="README.Design.Decidability.html#3529" class="Bound">A</a><a id="3556" class="Symbol">)</a> <a id="3558" class="Symbol">→</a> <a id="3560" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="3564" class="Symbol">(</a><a id="3565" href="README.Design.Decidability.html#3549" class="Bound">x</a> <a id="3567" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3569" href="README.Design.Decidability.html#3551" class="Bound">y</a><a id="3570" class="Symbol">))</a> <a id="3573" class="Keyword">where</a>

  <a id="ListDecEq₀._≟ᴸᴬ_"></a><a id="3582" href="README.Design.Decidability.html#3582" class="Function Operator">_≟ᴸᴬ_</a> <a id="3588" class="Symbol">:</a> <a id="3590" class="Symbol">(</a><a id="3591" href="README.Design.Decidability.html#3591" class="Bound">xs</a> <a id="3594" href="README.Design.Decidability.html#3594" class="Bound">ys</a> <a id="3597" class="Symbol">:</a> <a id="3599" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="3604" href="README.Design.Decidability.html#3529" class="Bound">A</a><a id="3605" class="Symbol">)</a> <a id="3607" class="Symbol">→</a> <a id="3609" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="3613" class="Symbol">(</a><a id="3614" href="README.Design.Decidability.html#3591" class="Bound">xs</a> <a id="3617" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3619" href="README.Design.Decidability.html#3594" class="Bound">ys</a><a id="3621" class="Symbol">)</a>
  <a id="3625" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="3634" href="README.Design.Decidability.html#3582" class="Function Operator">≟ᴸᴬ</a> <a id="3638" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="3647" class="Symbol">=</a> <a id="3649" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="3653" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
  <a id="3660" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="3669" href="README.Design.Decidability.html#3582" class="Function Operator">≟ᴸᴬ</a> <a id="3673" class="Symbol">(</a><a id="3674" href="README.Design.Decidability.html#3674" class="Bound">y</a> <a id="3676" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3678" href="README.Design.Decidability.html#3678" class="Bound">ys</a><a id="3680" class="Symbol">)</a> <a id="3682" class="Symbol">=</a> <a id="3684" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="3687" class="Symbol">λ</a> <a id="3689" class="Symbol">()</a>
  <a id="3694" class="Symbol">(</a><a id="3695" href="README.Design.Decidability.html#3695" class="Bound">x</a> <a id="3697" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3699" href="README.Design.Decidability.html#3699" class="Bound">xs</a><a id="3701" class="Symbol">)</a> <a id="3703" href="README.Design.Decidability.html#3582" class="Function Operator">≟ᴸᴬ</a> <a id="3707" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="3716" class="Symbol">=</a> <a id="3718" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="3721" class="Symbol">λ</a> <a id="3723" class="Symbol">()</a>
  <a id="3728" class="Symbol">(</a><a id="3729" href="README.Design.Decidability.html#3729" class="Bound">x</a> <a id="3731" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3733" href="README.Design.Decidability.html#3733" class="Bound">xs</a><a id="3735" class="Symbol">)</a> <a id="3737" href="README.Design.Decidability.html#3582" class="Function Operator">≟ᴸᴬ</a> <a id="3741" class="Symbol">(</a><a id="3742" href="README.Design.Decidability.html#3742" class="Bound">y</a> <a id="3744" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3746" href="README.Design.Decidability.html#3746" class="Bound">ys</a><a id="3748" class="Symbol">)</a> <a id="3750" class="Symbol">=</a>
    <a id="3756" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="3761" class="Symbol">(</a><a id="3762" href="Data.Product.Base.html#2609" class="Function">uncurry</a> <a id="3770" class="Symbol">(</a><a id="3771" href="Relation.Binary.PropositionalEquality.Core.html#1390" class="Function">cong₂</a> <a id="3777" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">_∷_</a><a id="3780" class="Symbol">))</a> <a id="3783" href="Data.List.Properties.html#2220" class="Function">∷-injective</a> <a id="3795" class="Symbol">(</a><a id="3796" href="README.Design.Decidability.html#3729" class="Bound">x</a> <a id="3798" href="README.Design.Decidability.html#3541" class="Bound Operator">≟ᴬ</a> <a id="3801" href="README.Design.Decidability.html#3742" class="Bound">y</a> <a id="3803" href="Relation.Nullary.Decidable.Core.html#2264" class="Function Operator">×-dec</a> <a id="3809" href="README.Design.Decidability.html#3733" class="Bound">xs</a> <a id="3812" href="README.Design.Decidability.html#3582" class="Function Operator">≟ᴸᴬ</a> <a id="3816" href="README.Design.Decidability.html#3746" class="Bound">ys</a><a id="3818" class="Symbol">)</a>

<a id="3821" class="Comment">-- The final case says that `x ∷ xs ≡ y ∷ ys` exactly when `x ≡ y` *and*</a>
<a id="3894" class="Comment">-- `xs ≡ ys`. The proofs are updated by the first two arguments to `map′`.</a>

<a id="3970" class="Comment">-- In the case of ≡-equality tests, the pattern</a>
<a id="4018" class="Comment">-- `map′ (congₙ c) c-injective (x₀ ≟ y₀ ×-dec ... ×-dec xₙ₋₁ ≟ yₙ₋₁)`</a>
<a id="4088" class="Comment">-- is captured by `≟-mapₙ n c c-injective (x₀ ≟ y₀) ... (xₙ₋₁ ≟ yₙ₋₁)`.</a>

<a id="4161" class="Keyword">module</a> <a id="ListDecEq₁"></a><a id="4168" href="README.Design.Decidability.html#4168" class="Module">ListDecEq₁</a> <a id="4179" class="Symbol">{</a><a id="4180" href="README.Design.Decidability.html#4180" class="Bound">a</a><a id="4181" class="Symbol">}</a> <a id="4183" class="Symbol">{</a><a id="4184" href="README.Design.Decidability.html#4184" class="Bound">A</a> <a id="4186" class="Symbol">:</a> <a id="4188" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="4192" href="README.Design.Decidability.html#4180" class="Bound">a</a><a id="4193" class="Symbol">}</a> <a id="4195" class="Symbol">(</a><a id="4196" href="README.Design.Decidability.html#4196" class="Bound Operator">_≟ᴬ_</a> <a id="4201" class="Symbol">:</a> <a id="4203" class="Symbol">(</a><a id="4204" href="README.Design.Decidability.html#4204" class="Bound">x</a> <a id="4206" href="README.Design.Decidability.html#4206" class="Bound">y</a> <a id="4208" class="Symbol">:</a> <a id="4210" href="README.Design.Decidability.html#4184" class="Bound">A</a><a id="4211" class="Symbol">)</a> <a id="4213" class="Symbol">→</a> <a id="4215" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="4219" class="Symbol">(</a><a id="4220" href="README.Design.Decidability.html#4204" class="Bound">x</a> <a id="4222" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="4224" href="README.Design.Decidability.html#4206" class="Bound">y</a><a id="4225" class="Symbol">))</a> <a id="4228" class="Keyword">where</a>

  <a id="ListDecEq₁._≟ᴸᴬ_"></a><a id="4237" href="README.Design.Decidability.html#4237" class="Function Operator">_≟ᴸᴬ_</a> <a id="4243" class="Symbol">:</a> <a id="4245" class="Symbol">(</a><a id="4246" href="README.Design.Decidability.html#4246" class="Bound">xs</a> <a id="4249" href="README.Design.Decidability.html#4249" class="Bound">ys</a> <a id="4252" class="Symbol">:</a> <a id="4254" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="4259" href="README.Design.Decidability.html#4184" class="Bound">A</a><a id="4260" class="Symbol">)</a> <a id="4262" class="Symbol">→</a> <a id="4264" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="4268" class="Symbol">(</a><a id="4269" href="README.Design.Decidability.html#4246" class="Bound">xs</a> <a id="4272" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="4274" href="README.Design.Decidability.html#4249" class="Bound">ys</a><a id="4276" class="Symbol">)</a>
  <a id="4280" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="4289" href="README.Design.Decidability.html#4237" class="Function Operator">≟ᴸᴬ</a> <a id="4293" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="4302" class="Symbol">=</a> <a id="4304" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="4308" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
  <a id="4315" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="4324" href="README.Design.Decidability.html#4237" class="Function Operator">≟ᴸᴬ</a> <a id="4328" class="Symbol">(</a><a id="4329" href="README.Design.Decidability.html#4329" class="Bound">y</a> <a id="4331" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="4333" href="README.Design.Decidability.html#4333" class="Bound">ys</a><a id="4335" class="Symbol">)</a> <a id="4337" class="Symbol">=</a> <a id="4339" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="4342" class="Symbol">λ</a> <a id="4344" class="Symbol">()</a>
  <a id="4349" class="Symbol">(</a><a id="4350" href="README.Design.Decidability.html#4350" class="Bound">x</a> <a id="4352" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="4354" href="README.Design.Decidability.html#4354" class="Bound">xs</a><a id="4356" class="Symbol">)</a> <a id="4358" href="README.Design.Decidability.html#4237" class="Function Operator">≟ᴸᴬ</a> <a id="4362" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="4371" class="Symbol">=</a> <a id="4373" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="4376" class="Symbol">λ</a> <a id="4378" class="Symbol">()</a>
  <a id="4383" class="Symbol">(</a><a id="4384" href="README.Design.Decidability.html#4384" class="Bound">x</a> <a id="4386" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="4388" href="README.Design.Decidability.html#4388" class="Bound">xs</a><a id="4390" class="Symbol">)</a> <a id="4392" href="README.Design.Decidability.html#4237" class="Function Operator">≟ᴸᴬ</a> <a id="4396" class="Symbol">(</a><a id="4397" href="README.Design.Decidability.html#4397" class="Bound">y</a> <a id="4399" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="4401" href="README.Design.Decidability.html#4401" class="Bound">ys</a><a id="4403" class="Symbol">)</a> <a id="4405" class="Symbol">=</a> <a id="4407" href="Relation.Nary.html#2978" class="Function">≟-mapₙ</a> <a id="4414" class="Number">2</a> <a id="4416" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">_∷_</a> <a id="4420" href="Data.List.Properties.html#2220" class="Function">∷-injective</a> <a id="4432" class="Symbol">(</a><a id="4433" href="README.Design.Decidability.html#4384" class="Bound">x</a> <a id="4435" href="README.Design.Decidability.html#4196" class="Bound Operator">≟ᴬ</a> <a id="4438" href="README.Design.Decidability.html#4397" class="Bound">y</a><a id="4439" class="Symbol">)</a> <a id="4441" class="Symbol">(</a><a id="4442" href="README.Design.Decidability.html#4388" class="Bound">xs</a> <a id="4445" href="README.Design.Decidability.html#4237" class="Function Operator">≟ᴸᴬ</a> <a id="4449" href="README.Design.Decidability.html#4401" class="Bound">ys</a><a id="4451" class="Symbol">)</a>
</pre></body></html>