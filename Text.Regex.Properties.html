<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Text.Regex.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Properties of regular expressions and their semantics</a>
<a id="163" class="Comment">------------------------------------------------------------------------</a>

<a id="237" class="Symbol">{-#</a> <a id="241" class="Keyword">OPTIONS</a> <a id="249" class="Pragma">--cubical-compatible</a> <a id="270" class="Pragma">--safe</a> <a id="277" class="Symbol">#-}</a>

<a id="282" class="Keyword">open</a> <a id="287" class="Keyword">import</a> <a id="294" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="310" class="Keyword">using</a> <a id="316" class="Symbol">(</a><a id="317" href="Relation.Binary.Bundles.html#3480" class="Record">DecPoset</a><a id="325" class="Symbol">)</a>

<a id="328" class="Keyword">module</a> <a id="335" href="Text.Regex.Properties.html" class="Module">Text.Regex.Properties</a> <a id="357" class="Symbol">{</a><a id="358" href="Text.Regex.Properties.html#358" class="Bound">a</a> <a id="360" href="Text.Regex.Properties.html#360" class="Bound">e</a> <a id="362" href="Text.Regex.Properties.html#362" class="Bound">r</a><a id="363" class="Symbol">}</a> <a id="365" class="Symbol">(</a><a id="366" href="Text.Regex.Properties.html#366" class="Bound">P?</a> <a id="369" class="Symbol">:</a> <a id="371" href="Relation.Binary.Bundles.html#3480" class="Record">DecPoset</a> <a id="380" href="Text.Regex.Properties.html#358" class="Bound">a</a> <a id="382" href="Text.Regex.Properties.html#360" class="Bound">e</a> <a id="384" href="Text.Regex.Properties.html#362" class="Bound">r</a><a id="385" class="Symbol">)</a> <a id="387" class="Keyword">where</a>

<a id="394" class="Keyword">open</a> <a id="399" class="Keyword">import</a> <a id="406" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="421" class="Keyword">using</a> <a id="427" class="Symbol">(</a><a id="428" href="Agda.Builtin.List.html#130" class="Datatype">List</a><a id="432" class="Symbol">;</a> <a id="434" href="Data.List.Base.html#7370" class="InductiveConstructor">[]</a><a id="436" class="Symbol">;</a> <a id="438" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">_∷_</a><a id="441" class="Symbol">)</a>
<a id="443" class="Keyword">open</a> <a id="448" class="Keyword">import</a> <a id="455" href="Data.List.Relation.Unary.All.html" class="Module">Data.List.Relation.Unary.All</a> <a id="484" class="Keyword">using</a> <a id="490" class="Symbol">(</a><a id="491" href="Data.List.Relation.Unary.All.html#6245" class="Function">all?</a><a id="495" class="Symbol">)</a>
<a id="497" class="Keyword">open</a> <a id="502" class="Keyword">import</a> <a id="509" href="Data.List.Relation.Unary.Any.html" class="Module">Data.List.Relation.Unary.Any</a> <a id="538" class="Keyword">using</a> <a id="544" class="Symbol">(</a><a id="545" href="Data.List.Relation.Unary.Any.html#2660" class="Function">any?</a><a id="549" class="Symbol">)</a>
<a id="551" class="Keyword">open</a> <a id="556" class="Keyword">import</a> <a id="563" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="581" class="Keyword">using</a> <a id="587" class="Symbol">(</a><a id="588" href="Data.Product.Base.html#1118" class="Function Operator">_×_</a><a id="591" class="Symbol">;</a> <a id="593" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="596" class="Symbol">;</a> <a id="598" href="Data.Product.Base.html#2609" class="Function">uncurry</a><a id="605" class="Symbol">)</a>
<a id="607" class="Keyword">open</a> <a id="612" class="Keyword">import</a> <a id="619" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="633" class="Keyword">using</a> <a id="639" class="Symbol">(</a><a id="640" href="Data.Sum.Base.html#625" class="Datatype Operator">_⊎_</a><a id="643" class="Symbol">;</a> <a id="645" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a><a id="649" class="Symbol">;</a> <a id="651" href="Data.Sum.Base.html#700" class="InductiveConstructor">inj₂</a><a id="655" class="Symbol">)</a>
<a id="657" class="Keyword">open</a> <a id="662" class="Keyword">import</a> <a id="669" href="Function.Base.html" class="Module">Function.Base</a> <a id="683" class="Keyword">using</a> <a id="689" class="Symbol">(</a><a id="690" href="Function.Base.html#2003" class="Function Operator">_$_</a><a id="693" class="Symbol">)</a>

<a id="696" class="Keyword">open</a> <a id="701" class="Keyword">import</a> <a id="708" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a>
  <a id="737" class="Keyword">using</a> <a id="743" class="Symbol">(</a><a id="744" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a><a id="747" class="Symbol">;</a> <a id="749" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a><a id="752" class="Symbol">;</a> <a id="754" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a><a id="756" class="Symbol">;</a> <a id="758" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a><a id="762" class="Symbol">;</a> <a id="764" href="Relation.Nullary.Decidable.Core.html#2172" class="Function">¬?</a><a id="766" class="Symbol">;</a> <a id="768" href="Relation.Nullary.Decidable.Core.html#2264" class="Function Operator">_×-dec_</a><a id="775" class="Symbol">;</a> <a id="777" href="Relation.Nullary.Decidable.Core.html#2394" class="Function Operator">_⊎-dec_</a><a id="784" class="Symbol">)</a>
<a id="786" class="Keyword">open</a> <a id="791" class="Keyword">import</a> <a id="798" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a>
  <a id="826" class="Keyword">using</a> <a id="832" class="Symbol">(</a><a id="833" href="Relation.Nullary.Negation.Core.html#824" class="Function Operator">¬_</a><a id="835" class="Symbol">;</a> <a id="837" href="Relation.Nullary.Negation.Core.html#1276" class="Function">contradiction</a><a id="850" class="Symbol">)</a>

<a id="853" class="Keyword">import</a> <a id="860" href="Relation.Unary.html" class="Module">Relation.Unary</a>  <a id="876" class="Symbol">as</a> <a id="879" class="Module">U</a>
<a id="881" class="Keyword">open</a> <a id="886" class="Keyword">import</a> <a id="893" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="909" class="Keyword">using</a> <a id="915" class="Symbol">(</a><a id="916" href="Relation.Binary.Definitions.html#5909" class="Function">Decidable</a><a id="925" class="Symbol">)</a>
<a id="927" class="Keyword">import</a> <a id="934" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="972" class="Symbol">as</a> <a id="975" class="Module">P</a>

<a id="978" class="Keyword">open</a> <a id="983" href="Relation.Binary.Bundles.html#3480" class="Module">DecPoset</a> <a id="992" href="Text.Regex.Properties.html#366" class="Bound">P?</a> <a id="995" class="Keyword">renaming</a> <a id="1004" class="Symbol">(</a><a id="1005" href="Relation.Binary.Bundles.html#3559" class="Field">Carrier</a> <a id="1013" class="Symbol">to</a> <a id="1016" class="Field">A</a><a id="1017" class="Symbol">)</a>
<a id="1019" class="Keyword">open</a> <a id="1024" class="Keyword">import</a> <a id="1031" href="Text.Regex.Base.html" class="Module">Text.Regex.Base</a> <a id="1047" href="Relation.Binary.Bundles.html#3332" class="Function">preorder</a>

<a id="1057" class="Keyword">open</a> <a id="1062" class="Keyword">import</a> <a id="1069" href="Data.List.Relation.Binary.Pointwise.html" class="Module">Data.List.Relation.Binary.Pointwise</a> <a id="1105" class="Keyword">using</a> <a id="1111" class="Symbol">(</a><a id="1112" href="Data.List.Relation.Binary.Pointwise.Base.html#893" class="InductiveConstructor">[]</a><a id="1114" class="Symbol">)</a>
<a id="1116" class="Keyword">open</a> <a id="1121" class="Keyword">import</a> <a id="1128" href="Data.List.Relation.Ternary.Appending.Propositional.html" class="Module">Data.List.Relation.Ternary.Appending.Propositional</a> <a id="1179" class="Symbol">{</a><a id="1180" class="Argument">A</a> <a id="1182" class="Symbol">=</a> <a id="1184" href="Text.Regex.Properties.html#1016" class="Field">A</a><a id="1185" class="Symbol">}</a>
<a id="1187" class="Keyword">open</a> <a id="1192" class="Keyword">import</a> <a id="1199" href="Data.List.Relation.Ternary.Appending.Propositional.Properties.html" class="Module">Data.List.Relation.Ternary.Appending.Propositional.Properties</a> <a id="1261" class="Symbol">{</a><a id="1262" class="Argument">A</a> <a id="1264" class="Symbol">=</a> <a id="1266" href="Text.Regex.Properties.html#1016" class="Field">A</a><a id="1267" class="Symbol">}</a>

<a id="1270" class="Comment">------------------------------------------------------------------------</a>
<a id="1343" class="Comment">-- Publicly re-export core properties</a>

<a id="1382" class="Keyword">open</a> <a id="1387" class="Keyword">import</a> <a id="1394" href="Text.Regex.Properties.Core.html" class="Module">Text.Regex.Properties.Core</a> <a id="1421" href="Relation.Binary.Bundles.html#3332" class="Function">preorder</a> <a id="1430" class="Keyword">public</a>

<a id="1438" class="Comment">------------------------------------------------------------------------</a>
<a id="1511" class="Comment">-- Decidability results</a>

<a id="[]∈?_"></a><a id="1536" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?_</a> <a id="1542" class="Symbol">:</a> <a id="1544" href="Relation.Unary.html#3694" class="Function">U.Decidable</a> <a id="1556" class="Symbol">(</a><a id="1557" class="InductiveConstructor">[]</a> <a id="1560" href="Text.Regex.Base.html#2380" class="Datatype Operator">∈_</a><a id="1562" class="Symbol">)</a>
<a id="1564" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1569" href="Text.Regex.Base.html#1290" class="InductiveConstructor">ε</a>       <a id="1577" class="Symbol">=</a> <a id="1579" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="1583" href="Text.Regex.Base.html#2425" class="InductiveConstructor">ε</a>
<a id="1585" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1590" href="Text.Regex.Base.html#1303" class="InductiveConstructor Operator">[</a> <a id="1592" href="Text.Regex.Properties.html#1592" class="Bound">rs</a> <a id="1595" href="Text.Regex.Base.html#1303" class="InductiveConstructor Operator">]</a>  <a id="1598" class="Symbol">=</a> <a id="1600" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="1603" class="Symbol">(λ</a> <a id="1606" class="Symbol">())</a>
<a id="1610" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1615" href="Text.Regex.Base.html#1336" class="InductiveConstructor Operator">[^</a> <a id="1618" href="Text.Regex.Properties.html#1618" class="Bound">rs</a> <a id="1621" href="Text.Regex.Base.html#1336" class="InductiveConstructor Operator">]</a> <a id="1623" class="Symbol">=</a> <a id="1625" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="1628" class="Symbol">(λ</a> <a id="1631" class="Symbol">())</a>
<a id="1635" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1640" class="Symbol">(</a><a id="1641" href="Text.Regex.Properties.html#1641" class="Bound">e</a> <a id="1643" href="Text.Regex.Base.html#1369" class="InductiveConstructor Operator">∣</a> <a id="1645" href="Text.Regex.Properties.html#1645" class="Bound">f</a><a id="1646" class="Symbol">)</a> <a id="1648" class="Symbol">=</a> <a id="1650" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="1655" href="Text.Regex.Base.html#2661" class="InductiveConstructor">sum</a> <a id="1659" class="Symbol">(λ</a> <a id="1662" class="Keyword">where</a> <a id="1668" class="Symbol">(</a><a id="1669" href="Text.Regex.Base.html#2661" class="InductiveConstructor">sum</a> <a id="1673" href="Text.Regex.Properties.html#1673" class="Bound">pr</a><a id="1675" class="Symbol">)</a> <a id="1677" class="Symbol">→</a> <a id="1679" href="Text.Regex.Properties.html#1673" class="Bound">pr</a><a id="1681" class="Symbol">)</a>
             <a id="1696" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="1698" class="Symbol">(</a><a id="1699" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1704" href="Text.Regex.Properties.html#1641" class="Bound">e</a><a id="1705" class="Symbol">)</a> <a id="1707" href="Relation.Nullary.Decidable.Core.html#2394" class="Function Operator">⊎-dec</a> <a id="1713" class="Symbol">(</a><a id="1714" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1719" href="Text.Regex.Properties.html#1645" class="Bound">f</a><a id="1720" class="Symbol">)</a>
<a id="1722" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1727" class="Symbol">(</a><a id="1728" href="Text.Regex.Properties.html#1728" class="Bound">e</a> <a id="1730" href="Text.Regex.Base.html#1396" class="InductiveConstructor Operator">∙</a> <a id="1732" href="Text.Regex.Properties.html#1732" class="Bound">f</a><a id="1733" class="Symbol">)</a> <a id="1735" class="Symbol">=</a> <a id="1737" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="1742" class="Symbol">(</a><a id="1743" href="Data.Product.Base.html#2609" class="Function">uncurry</a> <a id="1751" class="Symbol">(</a><a id="1752" href="Text.Regex.Base.html#2742" class="InductiveConstructor">prod</a> <a id="1757" class="Symbol">(</a><a id="1758" href="Data.List.Relation.Ternary.Appending.html#1074" class="InductiveConstructor Operator">[]++</a> <a id="1763" href="Data.List.Relation.Binary.Pointwise.Base.html#893" class="InductiveConstructor">[]</a><a id="1765" class="Symbol">)))</a> <a id="1769" href="Text.Regex.Properties.Core.html#2289" class="Function">[]∈e∙f-inv</a>
             <a id="1793" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="1795" class="Symbol">(</a><a id="1796" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1801" href="Text.Regex.Properties.html#1728" class="Bound">e</a><a id="1802" class="Symbol">)</a> <a id="1804" href="Relation.Nullary.Decidable.Core.html#2264" class="Function Operator">×-dec</a> <a id="1810" class="Symbol">(</a><a id="1811" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1816" href="Text.Regex.Properties.html#1732" class="Bound">f</a><a id="1817" class="Symbol">)</a>
<a id="1819" href="Text.Regex.Properties.html#1536" class="Function Operator">[]∈?</a> <a id="1824" class="Symbol">(</a><a id="1825" href="Text.Regex.Properties.html#1825" class="Bound">e</a> <a id="1827" href="Text.Regex.Base.html#1423" class="InductiveConstructor Operator">⋆</a><a id="1828" class="Symbol">)</a>   <a id="1832" class="Symbol">=</a> <a id="1834" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="1838" class="Symbol">(</a><a id="1839" href="Text.Regex.Base.html#2823" class="InductiveConstructor">star</a> <a id="1844" class="Symbol">(</a><a id="1845" href="Text.Regex.Base.html#2661" class="InductiveConstructor">sum</a> <a id="1849" class="Symbol">(</a><a id="1850" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a> <a id="1855" href="Text.Regex.Base.html#2425" class="InductiveConstructor">ε</a><a id="1856" class="Symbol">)))</a>

<a id="_∈ᴿ?_"></a><a id="1861" href="Text.Regex.Properties.html#1861" class="Function Operator">_∈ᴿ?_</a> <a id="1867" class="Symbol">:</a> <a id="1869" href="Relation.Binary.Definitions.html#5909" class="Function">Decidable</a> <a id="1879" href="Text.Regex.Base.html#2153" class="Datatype Operator">_∈ᴿ_</a>
<a id="1884" href="Text.Regex.Properties.html#1884" class="Bound">c</a> <a id="1886" href="Text.Regex.Properties.html#1861" class="Function Operator">∈ᴿ?</a> <a id="1890" href="Text.Regex.Base.html#1157" class="InductiveConstructor Operator">[</a> <a id="1892" href="Text.Regex.Properties.html#1892" class="Bound">a</a> <a id="1894" href="Text.Regex.Base.html#1157" class="InductiveConstructor Operator">]</a>     <a id="1900" class="Symbol">=</a> <a id="1902" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="1907" href="Text.Regex.Base.html#2200" class="InductiveConstructor Operator">[_]</a> <a id="1911" class="Symbol">(λ</a> <a id="1914" class="Keyword">where</a> <a id="1920" href="Text.Regex.Base.html#2200" class="InductiveConstructor Operator">[</a> <a id="1922" href="Text.Regex.Properties.html#1922" class="Bound">eq</a> <a id="1925" href="Text.Regex.Base.html#2200" class="InductiveConstructor Operator">]</a> <a id="1927" class="Symbol">→</a> <a id="1929" href="Text.Regex.Properties.html#1922" class="Bound">eq</a><a id="1931" class="Symbol">)</a> <a id="1933" class="Symbol">(</a><a id="1934" href="Text.Regex.Properties.html#1884" class="Bound">c</a> <a id="1936" href="Relation.Binary.Structures.html#3615" class="Function Operator">≟</a> <a id="1938" href="Text.Regex.Properties.html#1892" class="Bound">a</a><a id="1939" class="Symbol">)</a>
<a id="1941" href="Text.Regex.Properties.html#1941" class="Bound">c</a> <a id="1943" href="Text.Regex.Properties.html#1861" class="Function Operator">∈ᴿ?</a> <a id="1947" class="Symbol">(</a><a id="1948" href="Text.Regex.Properties.html#1948" class="Bound">lb</a> <a id="1951" href="Text.Regex.Base.html#1185" class="InductiveConstructor Operator">─</a> <a id="1953" href="Text.Regex.Properties.html#1953" class="Bound">ub</a><a id="1955" class="Symbol">)</a> <a id="1957" class="Symbol">=</a> <a id="1959" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="1964" class="Symbol">(</a><a id="1965" href="Data.Product.Base.html#2609" class="Function">uncurry</a> <a id="1973" href="Text.Regex.Base.html#2251" class="InductiveConstructor Operator">_─_</a><a id="1976" class="Symbol">)</a> <a id="1978" class="Symbol">(λ</a> <a id="1981" class="Keyword">where</a> <a id="1987" class="Symbol">(</a><a id="1988" href="Text.Regex.Properties.html#1988" class="Bound">ge</a> <a id="1991" href="Text.Regex.Base.html#2251" class="InductiveConstructor Operator">─</a> <a id="1993" href="Text.Regex.Properties.html#1993" class="Bound">le</a><a id="1995" class="Symbol">)</a> <a id="1997" class="Symbol">→</a> <a id="1999" href="Text.Regex.Properties.html#1988" class="Bound">ge</a> <a id="2002" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2004" href="Text.Regex.Properties.html#1993" class="Bound">le</a><a id="2006" class="Symbol">)</a>
                <a id="2024" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="2026" class="Symbol">(</a><a id="2027" href="Text.Regex.Properties.html#1948" class="Bound">lb</a> <a id="2030" href="Relation.Binary.Structures.html#3650" class="Function Operator">≤?</a> <a id="2033" href="Text.Regex.Properties.html#1941" class="Bound">c</a><a id="2034" class="Symbol">)</a> <a id="2036" href="Relation.Nullary.Decidable.Core.html#2264" class="Function Operator">×-dec</a> <a id="2042" class="Symbol">(</a><a id="2043" href="Text.Regex.Properties.html#1941" class="Bound">c</a> <a id="2045" href="Relation.Binary.Structures.html#3650" class="Function Operator">≤?</a> <a id="2048" href="Text.Regex.Properties.html#1953" class="Bound">ub</a><a id="2050" class="Symbol">)</a>

<a id="_∉ᴿ?_"></a><a id="2053" href="Text.Regex.Properties.html#2053" class="Function Operator">_∉ᴿ?_</a> <a id="2059" class="Symbol">:</a> <a id="2061" href="Relation.Binary.Definitions.html#5909" class="Function">Decidable</a> <a id="2071" href="Text.Regex.Base.html#2303" class="Function Operator">_∉ᴿ_</a>
<a id="2076" href="Text.Regex.Properties.html#2076" class="Bound">a</a> <a id="2078" href="Text.Regex.Properties.html#2053" class="Function Operator">∉ᴿ?</a> <a id="2082" href="Text.Regex.Properties.html#2082" class="Bound">r</a> <a id="2084" class="Symbol">=</a> <a id="2086" href="Relation.Nullary.Decidable.Core.html#2172" class="Function">¬?</a> <a id="2089" class="Symbol">(</a><a id="2090" href="Text.Regex.Properties.html#2076" class="Bound">a</a> <a id="2092" href="Text.Regex.Properties.html#1861" class="Function Operator">∈ᴿ?</a> <a id="2096" href="Text.Regex.Properties.html#2082" class="Bound">r</a><a id="2097" class="Symbol">)</a>

<a id="_∈?ε"></a><a id="2100" href="Text.Regex.Properties.html#2100" class="Function Operator">_∈?ε</a> <a id="2105" class="Symbol">:</a> <a id="2107" href="Relation.Unary.html#3694" class="Function">U.Decidable</a> <a id="2119" class="Symbol">(</a><a id="2120" href="Text.Regex.Base.html#2380" class="Datatype Operator">_∈</a> <a id="2123" class="InductiveConstructor">ε</a><a id="2124" class="Symbol">)</a>
<a id="2126" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>      <a id="2134" href="Text.Regex.Properties.html#2100" class="Function Operator">∈?ε</a> <a id="2138" class="Symbol">=</a> <a id="2140" href="Relation.Nullary.Decidable.Core.html#1622" class="InductiveConstructor">yes</a> <a id="2144" href="Text.Regex.Base.html#2425" class="InductiveConstructor">ε</a>
<a id="2146" class="Symbol">(</a><a id="2147" href="Text.Regex.Properties.html#2147" class="Bound">a</a> <a id="2149" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2151" class="Symbol">_)</a> <a id="2154" href="Text.Regex.Properties.html#2100" class="Function Operator">∈?ε</a> <a id="2158" class="Symbol">=</a> <a id="2160" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2163" class="Symbol">(λ</a> <a id="2166" class="Symbol">())</a>

<a id="_∈?[_]"></a><a id="2171" href="Text.Regex.Properties.html#2171" class="Function Operator">_∈?[_]</a> <a id="2178" class="Symbol">:</a> <a id="2180" class="Symbol">∀</a> <a id="2182" href="Text.Regex.Properties.html#2182" class="Bound">w</a> <a id="2184" href="Text.Regex.Properties.html#2184" class="Bound">rs</a> <a id="2187" class="Symbol">→</a> <a id="2189" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="2193" class="Symbol">(</a><a id="2194" href="Text.Regex.Properties.html#2182" class="Bound">w</a> <a id="2196" href="Text.Regex.Base.html#2380" class="Datatype Operator">∈</a> <a id="2198" class="InductiveConstructor Operator">[</a> <a id="2200" href="Text.Regex.Properties.html#2184" class="Bound">rs</a> <a id="2203" class="InductiveConstructor Operator">]</a><a id="2204" class="Symbol">)</a>
<a id="2206" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>          <a id="2218" href="Text.Regex.Properties.html#2171" class="Function Operator">∈?[</a> <a id="2222" href="Text.Regex.Properties.html#2222" class="Bound">rs</a> <a id="2225" href="Text.Regex.Properties.html#2171" class="Function Operator">]</a> <a id="2227" class="Symbol">=</a> <a id="2229" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2232" class="Symbol">(λ</a> <a id="2235" class="Symbol">())</a>
<a id="2239" class="Symbol">(</a><a id="2240" href="Text.Regex.Properties.html#2240" class="Bound">a</a> <a id="2242" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2244" href="Text.Regex.Properties.html#2244" class="Bound">b</a> <a id="2246" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2248" class="Symbol">_)</a> <a id="2251" href="Text.Regex.Properties.html#2171" class="Function Operator">∈?[</a> <a id="2255" href="Text.Regex.Properties.html#2255" class="Bound">rs</a> <a id="2258" href="Text.Regex.Properties.html#2171" class="Function Operator">]</a> <a id="2260" class="Symbol">=</a> <a id="2262" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2265" class="Symbol">(λ</a> <a id="2268" class="Symbol">())</a>
<a id="2272" class="Symbol">(</a><a id="2273" href="Text.Regex.Properties.html#2273" class="Bound">a</a> <a id="2275" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2277" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a><a id="2279" class="Symbol">)</a>    <a id="2284" href="Text.Regex.Properties.html#2171" class="Function Operator">∈?[</a> <a id="2288" href="Text.Regex.Properties.html#2288" class="Bound">rs</a> <a id="2291" href="Text.Regex.Properties.html#2171" class="Function Operator">]</a> <a id="2293" class="Symbol">=</a> <a id="2295" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="2300" href="Text.Regex.Base.html#2500" class="InductiveConstructor Operator">[_]</a> <a id="2304" class="Symbol">(λ</a> <a id="2307" class="Keyword">where</a> <a id="2313" href="Text.Regex.Base.html#2500" class="InductiveConstructor Operator">[</a> <a id="2315" href="Text.Regex.Properties.html#2315" class="Bound">p</a> <a id="2317" href="Text.Regex.Base.html#2500" class="InductiveConstructor Operator">]</a> <a id="2319" class="Symbol">→</a> <a id="2321" href="Text.Regex.Properties.html#2315" class="Bound">p</a><a id="2322" class="Symbol">)</a>
                      <a id="2346" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="2348" href="Data.List.Relation.Unary.Any.html#2660" class="Function">any?</a> <a id="2353" class="Symbol">(</a><a id="2354" href="Text.Regex.Properties.html#2273" class="Bound">a</a> <a id="2356" href="Text.Regex.Properties.html#1861" class="Function Operator">∈ᴿ?_</a><a id="2360" class="Symbol">)</a> <a id="2362" href="Text.Regex.Properties.html#2288" class="Bound">rs</a>

<a id="_∈?[^_]"></a><a id="2366" href="Text.Regex.Properties.html#2366" class="Function Operator">_∈?[^_]</a> <a id="2374" class="Symbol">:</a> <a id="2376" class="Symbol">∀</a> <a id="2378" href="Text.Regex.Properties.html#2378" class="Bound">w</a> <a id="2380" href="Text.Regex.Properties.html#2380" class="Bound">rs</a> <a id="2383" class="Symbol">→</a> <a id="2385" href="Relation.Nullary.Decidable.Core.html#1485" class="Record">Dec</a> <a id="2389" class="Symbol">(</a><a id="2390" href="Text.Regex.Properties.html#2378" class="Bound">w</a> <a id="2392" href="Text.Regex.Base.html#2380" class="Datatype Operator">∈</a> <a id="2394" class="InductiveConstructor Operator">[^</a> <a id="2397" href="Text.Regex.Properties.html#2380" class="Bound">rs</a> <a id="2400" class="InductiveConstructor Operator">]</a><a id="2401" class="Symbol">)</a>
<a id="2403" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>          <a id="2415" href="Text.Regex.Properties.html#2366" class="Function Operator">∈?[^</a> <a id="2420" href="Text.Regex.Properties.html#2420" class="Bound">rs</a> <a id="2423" href="Text.Regex.Properties.html#2366" class="Function Operator">]</a> <a id="2425" class="Symbol">=</a> <a id="2427" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2430" class="Symbol">(λ</a> <a id="2433" class="Symbol">())</a>
<a id="2437" class="Symbol">(</a><a id="2438" href="Text.Regex.Properties.html#2438" class="Bound">a</a> <a id="2440" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2442" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a><a id="2444" class="Symbol">)</a>    <a id="2449" href="Text.Regex.Properties.html#2366" class="Function Operator">∈?[^</a> <a id="2454" href="Text.Regex.Properties.html#2454" class="Bound">rs</a> <a id="2457" href="Text.Regex.Properties.html#2366" class="Function Operator">]</a> <a id="2459" class="Symbol">=</a> <a id="2461" href="Relation.Nullary.Decidable.Core.html#4788" class="Function">map′</a> <a id="2466" href="Text.Regex.Base.html#2580" class="InductiveConstructor Operator">[^_]</a> <a id="2471" class="Symbol">(λ</a> <a id="2474" class="Keyword">where</a> <a id="2480" href="Text.Regex.Base.html#2580" class="InductiveConstructor Operator">[^</a> <a id="2483" href="Text.Regex.Properties.html#2483" class="Bound">p</a> <a id="2485" href="Text.Regex.Base.html#2580" class="InductiveConstructor Operator">]</a> <a id="2487" class="Symbol">→</a> <a id="2489" href="Text.Regex.Properties.html#2483" class="Bound">p</a><a id="2490" class="Symbol">)</a> <a id="2492" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="2494" href="Data.List.Relation.Unary.All.html#6245" class="Function">all?</a> <a id="2499" class="Symbol">(</a><a id="2500" href="Text.Regex.Properties.html#2438" class="Bound">a</a> <a id="2502" href="Text.Regex.Properties.html#2053" class="Function Operator">∉ᴿ?_</a><a id="2506" class="Symbol">)</a> <a id="2508" href="Text.Regex.Properties.html#2454" class="Bound">rs</a>
<a id="2511" class="Symbol">(</a><a id="2512" href="Text.Regex.Properties.html#2512" class="Bound">a</a> <a id="2514" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2516" href="Text.Regex.Properties.html#2516" class="Bound">b</a> <a id="2518" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2520" class="Symbol">_)</a> <a id="2523" href="Text.Regex.Properties.html#2366" class="Function Operator">∈?[^</a> <a id="2528" href="Text.Regex.Properties.html#2528" class="Bound">rs</a> <a id="2531" href="Text.Regex.Properties.html#2366" class="Function Operator">]</a> <a id="2533" class="Symbol">=</a> <a id="2535" href="Relation.Nullary.Decidable.Core.html#1659" class="InductiveConstructor">no</a> <a id="2538" class="Symbol">(λ</a> <a id="2541" class="Symbol">())</a>
</pre></body></html>