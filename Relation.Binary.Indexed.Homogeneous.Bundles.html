<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Indexed.Homogeneous.Bundles</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Homogeneously-indexed binary relations</a>
<a id="148" class="Comment">------------------------------------------------------------------------</a>

<a id="222" class="Comment">-- The contents of this module should be accessed via</a>
<a id="276" class="Comment">-- `Relation.Binary.Indexed.Homogeneous`.</a>

<a id="319" class="Symbol">{-#</a> <a id="323" class="Keyword">OPTIONS</a> <a id="331" class="Pragma">--cubical-compatible</a> <a id="352" class="Pragma">--safe</a> <a id="359" class="Symbol">#-}</a>

<a id="364" class="Keyword">module</a> <a id="371" href="Relation.Binary.Indexed.Homogeneous.Bundles.html" class="Module">Relation.Binary.Indexed.Homogeneous.Bundles</a> <a id="415" class="Keyword">where</a>

<a id="422" class="Keyword">open</a> <a id="427" class="Keyword">import</a> <a id="434" href="Data.Product.html" class="Module">Data.Product</a> <a id="447" class="Keyword">using</a> <a id="453" class="Symbol">(</a><a id="454" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="457" class="Symbol">)</a>
<a id="459" class="Keyword">open</a> <a id="464" class="Keyword">import</a> <a id="471" href="Function.Base.html" class="Module">Function.Base</a> <a id="485" class="Keyword">using</a> <a id="491" class="Symbol">(</a><a id="492" href="Function.Base.html#4339" class="Function Operator">_⟨_⟩_</a><a id="497" class="Symbol">)</a>
<a id="499" class="Keyword">open</a> <a id="504" class="Keyword">import</a> <a id="511" href="Level.html" class="Module">Level</a> <a id="517" class="Keyword">using</a> <a id="523" class="Symbol">(</a><a id="524" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="529" class="Symbol">;</a> <a id="531" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="534" class="Symbol">;</a> <a id="536" href="Agda.Primitive.html#774" class="Primitive">suc</a><a id="539" class="Symbol">)</a>
<a id="541" class="Keyword">open</a> <a id="546" class="Keyword">import</a> <a id="553" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="569" class="Symbol">as</a> <a id="572" class="Module">B</a> <a id="574" class="Keyword">using</a> <a id="580" class="Symbol">(</a><a id="581" href="Relation.Binary.Core.html#1263" class="Function Operator">_⇒_</a><a id="584" class="Symbol">)</a>
<a id="586" class="Keyword">open</a> <a id="591" class="Keyword">import</a> <a id="598" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="636" class="Symbol">as</a> <a id="639" class="Module">P</a> <a id="641" class="Keyword">using</a> <a id="647" class="Symbol">(</a><a id="648" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="651" class="Symbol">)</a>
<a id="653" class="Keyword">open</a> <a id="658" class="Keyword">import</a> <a id="665" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="691" class="Keyword">using</a> <a id="697" class="Symbol">(</a><a id="698" href="Relation.Nullary.Negation.Core.html#824" class="Function Operator">¬_</a><a id="700" class="Symbol">)</a>
<a id="702" class="Keyword">open</a> <a id="707" class="Keyword">import</a> <a id="714" href="Relation.Binary.Indexed.Homogeneous.Core.html" class="Module">Relation.Binary.Indexed.Homogeneous.Core</a>
<a id="755" class="Keyword">open</a> <a id="760" class="Keyword">import</a> <a id="767" href="Relation.Binary.Indexed.Homogeneous.Structures.html" class="Module">Relation.Binary.Indexed.Homogeneous.Structures</a>

<a id="815" class="Comment">-- Indexed structures are laid out in a similar manner as to those</a>
<a id="882" class="Comment">-- in Relation.Binary. The main difference is each structure also</a>
<a id="948" class="Comment">-- contains proofs for the lifted version of the relation.</a>

<a id="1008" class="Comment">------------------------------------------------------------------------</a>
<a id="1081" class="Comment">-- Equivalences</a>

<a id="1098" class="Keyword">record</a> <a id="IndexedSetoid"></a><a id="1105" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1105" class="Record">IndexedSetoid</a> <a id="1119" class="Symbol">{</a><a id="1120" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1120" class="Bound">i</a><a id="1121" class="Symbol">}</a> <a id="1123" class="Symbol">(</a><a id="1124" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1124" class="Bound">I</a> <a id="1126" class="Symbol">:</a> <a id="1128" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1132" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1120" class="Bound">i</a><a id="1133" class="Symbol">)</a> <a id="1135" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1135" class="Bound">c</a> <a id="1137" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1137" class="Bound">ℓ</a> <a id="1139" class="Symbol">:</a> <a id="1141" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1145" class="Symbol">(</a><a id="1146" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="1150" class="Symbol">(</a><a id="1151" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1120" class="Bound">i</a> <a id="1153" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1155" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1135" class="Bound">c</a> <a id="1157" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1159" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1137" class="Bound">ℓ</a><a id="1160" class="Symbol">))</a> <a id="1163" class="Keyword">where</a>
  <a id="1171" class="Keyword">infix</a> <a id="1177" class="Number">4</a> <a id="1179" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1231" class="Field Operator">_≈ᵢ_</a> <a id="1184" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1422" class="Function Operator">_≈_</a>
  <a id="1190" class="Keyword">field</a>
    <a id="IndexedSetoid.Carrierᵢ"></a><a id="1200" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1200" class="Field">Carrierᵢ</a>       <a id="1215" class="Symbol">:</a> <a id="1217" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1124" class="Bound">I</a> <a id="1219" class="Symbol">→</a> <a id="1221" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1225" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1135" class="Bound">c</a>
    <a id="IndexedSetoid._≈ᵢ_"></a><a id="1231" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1231" class="Field Operator">_≈ᵢ_</a>           <a id="1246" class="Symbol">:</a> <a id="1248" href="Relation.Binary.Indexed.Homogeneous.Core.html#1027" class="Function">IRel</a> <a id="1253" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1200" class="Field">Carrierᵢ</a> <a id="1262" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1137" class="Bound">ℓ</a>
    <a id="IndexedSetoid.isEquivalenceᵢ"></a><a id="1268" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1268" class="Field">isEquivalenceᵢ</a> <a id="1283" class="Symbol">:</a> <a id="1285" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1200" class="Record">IsIndexedEquivalence</a> <a id="1306" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1200" class="Field">Carrierᵢ</a> <a id="1315" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1231" class="Field Operator">_≈ᵢ_</a>

  <a id="1323" class="Keyword">open</a> <a id="1328" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1200" class="Module">IsIndexedEquivalence</a> <a id="1349" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1268" class="Field">isEquivalenceᵢ</a> <a id="1364" class="Keyword">public</a>

  <a id="IndexedSetoid.Carrier"></a><a id="1374" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1374" class="Function">Carrier</a> <a id="1382" class="Symbol">:</a> <a id="1384" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1388" class="Symbol">_</a>
  <a id="1392" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1374" class="Function">Carrier</a> <a id="1400" class="Symbol">=</a> <a id="1402" class="Symbol">∀</a> <a id="1404" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1404" class="Bound">i</a> <a id="1406" class="Symbol">→</a> <a id="1408" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1200" class="Field">Carrierᵢ</a> <a id="1417" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1404" class="Bound">i</a>

  <a id="IndexedSetoid._≈_"></a><a id="1422" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1422" class="Function Operator">_≈_</a> <a id="1426" class="Symbol">:</a> <a id="1428" href="Relation.Binary.Core.html#891" class="Function">B.Rel</a> <a id="1434" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1374" class="Function">Carrier</a> <a id="1442" class="Symbol">_</a>
  <a id="1446" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1422" class="Function Operator">_≈_</a> <a id="1450" class="Symbol">=</a> <a id="1452" href="Relation.Binary.Indexed.Homogeneous.Core.html#1333" class="Function">Lift</a> <a id="1457" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1200" class="Field">Carrierᵢ</a> <a id="1466" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1231" class="Field Operator">_≈ᵢ_</a>

  <a id="IndexedSetoid._≉_"></a><a id="1474" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1474" class="Function Operator">_≉_</a> <a id="1478" class="Symbol">:</a> <a id="1480" href="Relation.Binary.Core.html#891" class="Function">B.Rel</a> <a id="1486" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1374" class="Function">Carrier</a> <a id="1494" class="Symbol">_</a>
  <a id="1498" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1498" class="Bound">x</a> <a id="1500" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1474" class="Function Operator">≉</a> <a id="1502" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1502" class="Bound">y</a> <a id="1504" class="Symbol">=</a> <a id="1506" href="Relation.Nullary.Negation.Core.html#824" class="Function Operator">¬</a> <a id="1508" class="Symbol">(</a><a id="1509" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1498" class="Bound">x</a> <a id="1511" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1422" class="Function Operator">≈</a> <a id="1513" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1502" class="Bound">y</a><a id="1514" class="Symbol">)</a>

  <a id="IndexedSetoid.setoid"></a><a id="1519" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1519" class="Function">setoid</a> <a id="1526" class="Symbol">:</a> <a id="1528" href="Relation.Binary.Bundles.html#1027" class="Record">B.Setoid</a> <a id="1537" class="Symbol">_</a> <a id="1539" class="Symbol">_</a>
  <a id="1543" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1519" class="Function">setoid</a> <a id="1550" class="Symbol">=</a> <a id="1552" class="Keyword">record</a>
    <a id="1563" class="Symbol">{</a> <a id="1565" href="Relation.Binary.Bundles.html#1150" class="Field">isEquivalence</a> <a id="1579" class="Symbol">=</a> <a id="1581" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1704" class="Function">isEquivalence</a>
    <a id="1599" class="Symbol">}</a>


<a id="1603" class="Keyword">record</a> <a id="IndexedDecSetoid"></a><a id="1610" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1610" class="Record">IndexedDecSetoid</a> <a id="1627" class="Symbol">{</a><a id="1628" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1628" class="Bound">i</a><a id="1629" class="Symbol">}</a> <a id="1631" class="Symbol">(</a><a id="1632" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1632" class="Bound">I</a> <a id="1634" class="Symbol">:</a> <a id="1636" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1640" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1628" class="Bound">i</a><a id="1641" class="Symbol">)</a> <a id="1643" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1643" class="Bound">c</a> <a id="1645" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1645" class="Bound">ℓ</a> <a id="1647" class="Symbol">:</a> <a id="1649" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1653" class="Symbol">(</a><a id="1654" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="1658" class="Symbol">(</a><a id="1659" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1628" class="Bound">i</a> <a id="1661" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1663" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1643" class="Bound">c</a> <a id="1665" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1667" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1645" class="Bound">ℓ</a><a id="1668" class="Symbol">))</a> <a id="1671" class="Keyword">where</a>
  <a id="1679" class="Keyword">infix</a> <a id="1685" class="Number">4</a> <a id="1687" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1738" class="Field Operator">_≈ᵢ_</a>
  <a id="1694" class="Keyword">field</a>
    <a id="IndexedDecSetoid.Carrierᵢ"></a><a id="1704" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1704" class="Field">Carrierᵢ</a>          <a id="1722" class="Symbol">:</a> <a id="1724" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1632" class="Bound">I</a> <a id="1726" class="Symbol">→</a> <a id="1728" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1732" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1643" class="Bound">c</a>
    <a id="IndexedDecSetoid._≈ᵢ_"></a><a id="1738" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1738" class="Field Operator">_≈ᵢ_</a>              <a id="1756" class="Symbol">:</a> <a id="1758" href="Relation.Binary.Indexed.Homogeneous.Core.html#1027" class="Function">IRel</a> <a id="1763" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1704" class="Field">Carrierᵢ</a> <a id="1772" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1645" class="Bound">ℓ</a>
    <a id="IndexedDecSetoid.isDecEquivalenceᵢ"></a><a id="1778" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1778" class="Field">isDecEquivalenceᵢ</a> <a id="1796" class="Symbol">:</a> <a id="1798" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1847" class="Record">IsIndexedDecEquivalence</a> <a id="1822" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1704" class="Field">Carrierᵢ</a> <a id="1831" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1738" class="Field Operator">_≈ᵢ_</a>

  <a id="1839" class="Keyword">open</a> <a id="1844" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1847" class="Module">IsIndexedDecEquivalence</a> <a id="1868" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1778" class="Field">isDecEquivalenceᵢ</a> <a id="1886" class="Keyword">public</a>

  <a id="IndexedDecSetoid.indexedSetoid"></a><a id="1896" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1896" class="Function">indexedSetoid</a> <a id="1910" class="Symbol">:</a> <a id="1912" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1105" class="Record">IndexedSetoid</a> <a id="1926" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1632" class="Bound">I</a> <a id="1928" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1643" class="Bound">c</a> <a id="1930" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1645" class="Bound">ℓ</a>
  <a id="1934" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1896" class="Function">indexedSetoid</a> <a id="1948" class="Symbol">=</a> <a id="1950" class="Keyword">record</a>
    <a id="1961" class="Symbol">{</a> <a id="1963" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1268" class="Field">isEquivalenceᵢ</a> <a id="1978" class="Symbol">=</a> <a id="1980" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1960" class="Function">isEquivalenceᵢ</a>
    <a id="1999" class="Symbol">}</a>

  <a id="2004" class="Keyword">open</a> <a id="2009" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1105" class="Module">IndexedSetoid</a> <a id="2023" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1896" class="Function">indexedSetoid</a> <a id="2037" class="Keyword">public</a>
    <a id="2048" class="Keyword">using</a> <a id="2054" class="Symbol">(</a><a id="2055" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1374" class="Function">Carrier</a><a id="2062" class="Symbol">;</a> <a id="2064" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1422" class="Function Operator">_≈_</a><a id="2067" class="Symbol">;</a> <a id="2069" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1474" class="Function Operator">_≉_</a><a id="2072" class="Symbol">;</a> <a id="2074" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1519" class="Function">setoid</a><a id="2080" class="Symbol">)</a>

<a id="2083" class="Comment">------------------------------------------------------------------------</a>
<a id="2156" class="Comment">-- Preorders</a>

<a id="2170" class="Keyword">record</a> <a id="IndexedPreorder"></a><a id="2177" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2177" class="Record">IndexedPreorder</a> <a id="2193" class="Symbol">{</a><a id="2194" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2194" class="Bound">i</a><a id="2195" class="Symbol">}</a> <a id="2197" class="Symbol">(</a><a id="2198" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2198" class="Bound">I</a> <a id="2200" class="Symbol">:</a> <a id="2202" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2206" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2194" class="Bound">i</a><a id="2207" class="Symbol">)</a> <a id="2209" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2209" class="Bound">c</a> <a id="2211" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2211" class="Bound">ℓ₁</a> <a id="2214" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2214" class="Bound">ℓ₂</a> <a id="2217" class="Symbol">:</a>
                       <a id="2242" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2246" class="Symbol">(</a><a id="2247" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="2251" class="Symbol">(</a><a id="2252" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2194" class="Bound">i</a> <a id="2254" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2256" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2209" class="Bound">c</a> <a id="2258" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2260" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2211" class="Bound">ℓ₁</a> <a id="2263" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2265" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2214" class="Bound">ℓ₂</a><a id="2267" class="Symbol">))</a> <a id="2270" class="Keyword">where</a>

  <a id="2279" class="Keyword">infix</a> <a id="2285" class="Number">4</a> <a id="2287" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2346" class="Field Operator">_≈ᵢ_</a> <a id="2292" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2381" class="Field Operator">_∼ᵢ_</a> <a id="2297" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2563" class="Function Operator">_≈_</a> <a id="2301" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2615" class="Function Operator">_∼_</a>

  <a id="2308" class="Keyword">field</a>
    <a id="IndexedPreorder.Carrierᵢ"></a><a id="2318" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2318" class="Field">Carrierᵢ</a>    <a id="2330" class="Symbol">:</a> <a id="2332" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2198" class="Bound">I</a> <a id="2334" class="Symbol">→</a> <a id="2336" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2340" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2209" class="Bound">c</a>
    <a id="IndexedPreorder._≈ᵢ_"></a><a id="2346" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2346" class="Field Operator">_≈ᵢ_</a>        <a id="2358" class="Symbol">:</a> <a id="2360" href="Relation.Binary.Indexed.Homogeneous.Core.html#1027" class="Function">IRel</a> <a id="2365" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2318" class="Field">Carrierᵢ</a> <a id="2374" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2211" class="Bound">ℓ₁</a>
    <a id="IndexedPreorder._∼ᵢ_"></a><a id="2381" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2381" class="Field Operator">_∼ᵢ_</a>        <a id="2393" class="Symbol">:</a> <a id="2395" href="Relation.Binary.Indexed.Homogeneous.Core.html#1027" class="Function">IRel</a> <a id="2400" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2318" class="Field">Carrierᵢ</a> <a id="2409" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2214" class="Bound">ℓ₂</a>
    <a id="IndexedPreorder.isPreorderᵢ"></a><a id="2416" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Field">isPreorderᵢ</a> <a id="2428" class="Symbol">:</a> <a id="2430" href="Relation.Binary.Indexed.Homogeneous.Structures.html#2145" class="Record">IsIndexedPreorder</a> <a id="2448" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2318" class="Field">Carrierᵢ</a> <a id="2457" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2346" class="Field Operator">_≈ᵢ_</a> <a id="2462" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2381" class="Field Operator">_∼ᵢ_</a>

  <a id="2470" class="Keyword">open</a> <a id="2475" href="Relation.Binary.Indexed.Homogeneous.Structures.html#2145" class="Module">IsIndexedPreorder</a> <a id="2493" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Field">isPreorderᵢ</a> <a id="2505" class="Keyword">public</a>

  <a id="IndexedPreorder.Carrier"></a><a id="2515" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2515" class="Function">Carrier</a> <a id="2523" class="Symbol">:</a> <a id="2525" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2529" class="Symbol">_</a>
  <a id="2533" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2515" class="Function">Carrier</a> <a id="2541" class="Symbol">=</a> <a id="2543" class="Symbol">∀</a> <a id="2545" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2545" class="Bound">i</a> <a id="2547" class="Symbol">→</a> <a id="2549" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2318" class="Field">Carrierᵢ</a> <a id="2558" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2545" class="Bound">i</a>

  <a id="IndexedPreorder._≈_"></a><a id="2563" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2563" class="Function Operator">_≈_</a> <a id="2567" class="Symbol">:</a> <a id="2569" href="Relation.Binary.Core.html#891" class="Function">B.Rel</a> <a id="2575" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2515" class="Function">Carrier</a> <a id="2583" class="Symbol">_</a>
  <a id="2587" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2587" class="Bound">x</a> <a id="2589" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2563" class="Function Operator">≈</a> <a id="2591" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2591" class="Bound">y</a> <a id="2593" class="Symbol">=</a> <a id="2595" class="Symbol">∀</a> <a id="2597" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2597" class="Bound">i</a> <a id="2599" class="Symbol">→</a> <a id="2601" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2587" class="Bound">x</a> <a id="2603" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2597" class="Bound">i</a> <a id="2605" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2346" class="Field Operator">≈ᵢ</a> <a id="2608" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2591" class="Bound">y</a> <a id="2610" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2597" class="Bound">i</a>

  <a id="IndexedPreorder._∼_"></a><a id="2615" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2615" class="Function Operator">_∼_</a> <a id="2619" class="Symbol">:</a> <a id="2621" href="Relation.Binary.Core.html#891" class="Function">B.Rel</a> <a id="2627" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2515" class="Function">Carrier</a> <a id="2635" class="Symbol">_</a>
  <a id="2639" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2639" class="Bound">x</a> <a id="2641" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2615" class="Function Operator">∼</a> <a id="2643" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2643" class="Bound">y</a> <a id="2645" class="Symbol">=</a> <a id="2647" class="Symbol">∀</a> <a id="2649" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2649" class="Bound">i</a> <a id="2651" class="Symbol">→</a> <a id="2653" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2639" class="Bound">x</a> <a id="2655" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2649" class="Bound">i</a> <a id="2657" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2381" class="Field Operator">∼ᵢ</a> <a id="2660" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2643" class="Bound">y</a> <a id="2662" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2649" class="Bound">i</a>

  <a id="IndexedPreorder.preorder"></a><a id="2667" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2667" class="Function">preorder</a> <a id="2676" class="Symbol">:</a> <a id="2678" href="Relation.Binary.Bundles.html#1938" class="Record">B.Preorder</a> <a id="2689" class="Symbol">_</a> <a id="2691" class="Symbol">_</a> <a id="2693" class="Symbol">_</a>
  <a id="2697" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2667" class="Function">preorder</a> <a id="2706" class="Symbol">=</a> <a id="2708" class="Keyword">record</a>
    <a id="2719" class="Symbol">{</a> <a id="2721" href="Relation.Binary.Bundles.html#2151" class="Field">isPreorder</a> <a id="2732" class="Symbol">=</a> <a id="2734" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3292" class="Function">isPreorder</a>
    <a id="2749" class="Symbol">}</a>

<a id="2752" class="Comment">------------------------------------------------------------------------</a>
<a id="2825" class="Comment">-- Partial orders</a>

<a id="2844" class="Keyword">record</a> <a id="IndexedPoset"></a><a id="2851" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2851" class="Record">IndexedPoset</a> <a id="2864" class="Symbol">{</a><a id="2865" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2865" class="Bound">i</a><a id="2866" class="Symbol">}</a> <a id="2868" class="Symbol">(</a><a id="2869" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2869" class="Bound">I</a> <a id="2871" class="Symbol">:</a> <a id="2873" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2877" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2865" class="Bound">i</a><a id="2878" class="Symbol">)</a> <a id="2880" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2880" class="Bound">c</a> <a id="2882" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2882" class="Bound">ℓ₁</a> <a id="2885" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2885" class="Bound">ℓ₂</a> <a id="2888" class="Symbol">:</a>
                    <a id="2910" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2914" class="Symbol">(</a><a id="2915" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="2919" class="Symbol">(</a><a id="2920" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2865" class="Bound">i</a> <a id="2922" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2924" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2880" class="Bound">c</a> <a id="2926" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2928" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2882" class="Bound">ℓ₁</a> <a id="2931" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2933" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2885" class="Bound">ℓ₂</a><a id="2935" class="Symbol">))</a> <a id="2938" class="Keyword">where</a>
  <a id="2946" class="Keyword">field</a>
    <a id="IndexedPoset.Carrierᵢ"></a><a id="2956" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2956" class="Field">Carrierᵢ</a>        <a id="2972" class="Symbol">:</a> <a id="2974" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2869" class="Bound">I</a> <a id="2976" class="Symbol">→</a> <a id="2978" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2982" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2880" class="Bound">c</a>
    <a id="IndexedPoset._≈ᵢ_"></a><a id="2988" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2988" class="Field Operator">_≈ᵢ_</a>            <a id="3004" class="Symbol">:</a> <a id="3006" href="Relation.Binary.Indexed.Homogeneous.Core.html#1027" class="Function">IRel</a> <a id="3011" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2956" class="Field">Carrierᵢ</a> <a id="3020" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2882" class="Bound">ℓ₁</a>
    <a id="IndexedPoset._≤ᵢ_"></a><a id="3027" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3027" class="Field Operator">_≤ᵢ_</a>            <a id="3043" class="Symbol">:</a> <a id="3045" href="Relation.Binary.Indexed.Homogeneous.Core.html#1027" class="Function">IRel</a> <a id="3050" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2956" class="Field">Carrierᵢ</a> <a id="3059" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2885" class="Bound">ℓ₂</a>
    <a id="IndexedPoset.isPartialOrderᵢ"></a><a id="3066" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3066" class="Field">isPartialOrderᵢ</a> <a id="3082" class="Symbol">:</a> <a id="3084" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3574" class="Record">IsIndexedPartialOrder</a> <a id="3106" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2956" class="Field">Carrierᵢ</a> <a id="3115" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2988" class="Field Operator">_≈ᵢ_</a> <a id="3120" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3027" class="Field Operator">_≤ᵢ_</a>

  <a id="3128" class="Keyword">open</a> <a id="3133" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3574" class="Module">IsIndexedPartialOrder</a> <a id="3155" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3066" class="Field">isPartialOrderᵢ</a> <a id="3171" class="Keyword">public</a>

  <a id="IndexedPoset.preorderᵢ"></a><a id="3181" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3181" class="Function">preorderᵢ</a> <a id="3191" class="Symbol">:</a> <a id="3193" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2177" class="Record">IndexedPreorder</a> <a id="3209" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2869" class="Bound">I</a> <a id="3211" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2880" class="Bound">c</a> <a id="3213" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2882" class="Bound">ℓ₁</a> <a id="3216" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2885" class="Bound">ℓ₂</a>
  <a id="3221" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3181" class="Function">preorderᵢ</a> <a id="3231" class="Symbol">=</a> <a id="3233" class="Keyword">record</a>
    <a id="3244" class="Symbol">{</a> <a id="3246" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Field">isPreorderᵢ</a> <a id="3258" class="Symbol">=</a> <a id="3260" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3688" class="Function">isPreorderᵢ</a>
    <a id="3276" class="Symbol">}</a>

  <a id="3281" class="Keyword">open</a> <a id="3286" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2177" class="Module">IndexedPreorder</a> <a id="3302" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3181" class="Function">preorderᵢ</a> <a id="3312" class="Keyword">public</a>
    <a id="3323" class="Keyword">using</a> <a id="3329" class="Symbol">(</a><a id="3330" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2515" class="Function">Carrier</a><a id="3337" class="Symbol">;</a> <a id="3339" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2563" class="Function Operator">_≈_</a><a id="3342" class="Symbol">;</a> <a id="3344" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2667" class="Function">preorder</a><a id="3352" class="Symbol">)</a> <a id="3354" class="Keyword">renaming</a> <a id="3363" class="Symbol">(</a><a id="3364" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2615" class="Function Operator">_∼_</a> <a id="3368" class="Symbol">to</a> <a id="3371" class="Function Operator">_≤_</a><a id="3374" class="Symbol">)</a>

  <a id="IndexedPoset.poset"></a><a id="3379" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3379" class="Function">poset</a> <a id="3385" class="Symbol">:</a> <a id="3387" href="Relation.Binary.Bundles.html#3046" class="Record">B.Poset</a> <a id="3395" class="Symbol">_</a> <a id="3397" class="Symbol">_</a> <a id="3399" class="Symbol">_</a>
  <a id="3403" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3379" class="Function">poset</a> <a id="3409" class="Symbol">=</a> <a id="3411" class="Keyword">record</a>
    <a id="3422" class="Symbol">{</a> <a id="3424" href="Relation.Binary.Bundles.html#3221" class="Field">isPartialOrder</a> <a id="3439" class="Symbol">=</a> <a id="3441" href="Relation.Binary.Indexed.Homogeneous.Structures.html#4130" class="Function">isPartialOrder</a>
    <a id="3460" class="Symbol">}</a>
</pre></body></html>