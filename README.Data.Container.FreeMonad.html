<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Data.Container.FreeMonad</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Example showing how the free monad construction on containers can be</a>
<a id="178" class="Comment">-- used</a>
<a id="186" class="Comment">------------------------------------------------------------------------</a>

<a id="260" class="Symbol">{-#</a> <a id="264" class="Keyword">OPTIONS</a> <a id="272" class="Pragma">--cubical-compatible</a> <a id="293" class="Pragma">--sized-types</a> <a id="307" class="Symbol">#-}</a>

<a id="312" class="Keyword">module</a> <a id="319" href="README.Data.Container.FreeMonad.html" class="Module">README.Data.Container.FreeMonad</a> <a id="351" class="Keyword">where</a>

<a id="358" class="Keyword">open</a> <a id="363" class="Keyword">import</a> <a id="370" href="Level.html" class="Module">Level</a> <a id="376" class="Keyword">using</a> <a id="382" class="Symbol">(</a><a id="383" href="Level.html#521" class="Function">0ℓ</a><a id="385" class="Symbol">)</a>
<a id="387" class="Keyword">open</a> <a id="392" class="Keyword">import</a> <a id="399" href="Effect.Monad.html" class="Module">Effect.Monad</a>
<a id="412" class="Keyword">open</a> <a id="417" class="Keyword">import</a> <a id="424" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="435" class="Keyword">open</a> <a id="440" class="Keyword">import</a> <a id="447" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="457" class="Keyword">open</a> <a id="462" class="Keyword">import</a> <a id="469" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="484" class="Keyword">using</a> <a id="490" class="Symbol">(</a><a id="491" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a><a id="495" class="Symbol">;</a> <a id="497" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a><a id="501" class="Symbol">)</a>
<a id="503" class="Keyword">open</a> <a id="508" class="Keyword">import</a> <a id="515" href="Data.Nat.html" class="Module">Data.Nat</a>
<a id="524" class="Keyword">open</a> <a id="529" class="Keyword">import</a> <a id="536" href="Data.Sum.html" class="Module">Data.Sum</a> <a id="545" class="Keyword">using</a> <a id="551" class="Symbol">(</a><a id="552" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a><a id="556" class="Symbol">;</a> <a id="558" href="Data.Sum.Base.html#700" class="InductiveConstructor">inj₂</a><a id="562" class="Symbol">)</a>
<a id="564" class="Keyword">open</a> <a id="569" class="Keyword">import</a> <a id="576" href="Data.Product.html" class="Module">Data.Product</a> <a id="589" class="Keyword">renaming</a> <a id="598" class="Symbol">(</a><a id="599" href="Data.Product.Base.html#1118" class="Function Operator">_×_</a> <a id="603" class="Symbol">to</a> <a id="606" class="Function Operator">_⟨×⟩_</a><a id="611" class="Symbol">)</a>
<a id="613" class="Keyword">open</a> <a id="618" class="Keyword">import</a> <a id="625" href="Data.Container.html" class="Module">Data.Container</a> <a id="640" class="Keyword">using</a> <a id="646" class="Symbol">(</a><a id="647" href="Data.Container.Core.html#567" class="Record">Container</a><a id="656" class="Symbol">;</a> <a id="658" href="Data.Container.Core.html#631" class="InductiveConstructor Operator">_▷_</a><a id="661" class="Symbol">)</a>
<a id="663" class="Keyword">open</a> <a id="668" class="Keyword">import</a> <a id="675" href="Data.Container.Combinator.html" class="Module">Data.Container.Combinator</a>
<a id="701" class="Keyword">open</a> <a id="706" class="Keyword">import</a> <a id="713" href="Data.Container.FreeMonad.html" class="Module">Data.Container.FreeMonad</a> <a id="738" class="Symbol">as</a> <a id="741" class="Module">FreeMonad</a>
<a id="751" class="Keyword">open</a> <a id="756" class="Keyword">import</a> <a id="763" href="Data.W.html" class="Module">Data.W</a>
<a id="770" class="Keyword">open</a> <a id="775" class="Keyword">import</a> <a id="782" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="820" class="Symbol">as</a> <a id="823" class="Module">P</a>

<a id="826" class="Comment">------------------------------------------------------------------------</a>
<a id="899" class="Comment">-- Defining the signature of an effect and building trees describing</a>
<a id="968" class="Comment">-- computations leveraging that effect.</a>

<a id="1009" class="Comment">-- The signature of state and its (generic) operations.</a>

<a id="State"></a><a id="1066" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1072" class="Symbol">:</a> <a id="1074" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1078" class="Symbol">→</a> <a id="1080" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="1090" class="Symbol">_</a> <a id="1092" class="Symbol">_</a>
<a id="1094" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1100" href="README.Data.Container.FreeMonad.html#1100" class="Bound">S</a> <a id="1102" class="Symbol">=</a> <a id="1104" href="Agda.Builtin.Unit.html#158" class="Record">⊤</a> <a id="1106" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">⟶</a> <a id="1108" href="README.Data.Container.FreeMonad.html#1100" class="Bound">S</a> <a id="1110" href="Data.Container.Combinator.html#3119" class="Function Operator">⊎</a> <a id="1112" href="README.Data.Container.FreeMonad.html#1100" class="Bound">S</a> <a id="1114" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">⟶</a> <a id="1116" href="Agda.Builtin.Unit.html#158" class="Record">⊤</a>
  <a id="1120" class="Keyword">where</a>
  <a id="1128" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">_⟶_</a> <a id="1132" class="Symbol">:</a> <a id="1134" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1138" class="Symbol">→</a> <a id="1140" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1144" class="Symbol">→</a> <a id="1146" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="1156" class="Symbol">_</a> <a id="1158" class="Symbol">_</a>
  <a id="1162" href="README.Data.Container.FreeMonad.html#1162" class="Bound">I</a> <a id="1164" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">⟶</a> <a id="1166" href="README.Data.Container.FreeMonad.html#1166" class="Bound">O</a> <a id="1168" class="Symbol">=</a> <a id="1170" href="README.Data.Container.FreeMonad.html#1162" class="Bound">I</a> <a id="1172" href="Data.Container.Core.html#631" class="InductiveConstructor Operator">▷</a> <a id="1174" class="Symbol">λ</a> <a id="1176" href="README.Data.Container.FreeMonad.html#1176" class="Bound">_</a> <a id="1178" class="Symbol">→</a> <a id="1180" href="README.Data.Container.FreeMonad.html#1166" class="Bound">O</a>

<a id="get"></a><a id="1183" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a> <a id="1187" class="Symbol">:</a> <a id="1189" class="Symbol">∀</a> <a id="1191" class="Symbol">{</a><a id="1192" href="README.Data.Container.FreeMonad.html#1192" class="Bound">S</a><a id="1193" class="Symbol">}</a> <a id="1195" class="Symbol">→</a> <a id="1197" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1203" href="README.Data.Container.FreeMonad.html#1192" class="Bound">S</a> <a id="1205" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1207" href="README.Data.Container.FreeMonad.html#1192" class="Bound">S</a>
<a id="1209" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a> <a id="1213" class="Symbol">=</a> <a id="1215" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1222" class="Symbol">(</a><a id="1223" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a> <a id="1228" class="Symbol">_</a> <a id="1230" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1232" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a><a id="1236" class="Symbol">)</a>

<a id="put"></a><a id="1239" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="1243" class="Symbol">:</a> <a id="1245" class="Symbol">∀</a> <a id="1247" class="Symbol">{</a><a id="1248" href="README.Data.Container.FreeMonad.html#1248" class="Bound">S</a><a id="1249" class="Symbol">}</a> <a id="1251" class="Symbol">→</a> <a id="1253" href="README.Data.Container.FreeMonad.html#1248" class="Bound">S</a> <a id="1255" class="Symbol">→</a> <a id="1257" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1263" href="README.Data.Container.FreeMonad.html#1248" class="Bound">S</a> <a id="1265" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1267" href="Agda.Builtin.Unit.html#158" class="Record">⊤</a>
<a id="1269" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="1273" href="README.Data.Container.FreeMonad.html#1273" class="Bound">s</a> <a id="1275" class="Symbol">=</a> <a id="1277" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1284" class="Symbol">(</a><a id="1285" href="Data.Sum.Base.html#700" class="InductiveConstructor">inj₂</a> <a id="1290" href="README.Data.Container.FreeMonad.html#1273" class="Bound">s</a> <a id="1292" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1294" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a><a id="1298" class="Symbol">)</a>

<a id="1301" class="Comment">-- Using the above we can, for example, write a stateful program that</a>
<a id="1371" class="Comment">-- delivers a boolean.</a>
<a id="prog"></a><a id="1394" href="README.Data.Container.FreeMonad.html#1394" class="Function">prog</a> <a id="1399" class="Symbol">:</a> <a id="1401" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1407" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1409" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1411" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a>
<a id="1416" href="README.Data.Container.FreeMonad.html#1394" class="Function">prog</a> <a id="1421" class="Symbol">=</a>
  <a id="1425" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a>         <a id="1437" href="Data.Container.FreeMonad.html#3830" class="Function Operator">&gt;&gt;=</a> <a id="1441" class="Symbol">λ</a> <a id="1443" href="README.Data.Container.FreeMonad.html#1443" class="Bound">n</a> <a id="1445" class="Symbol">→</a>
  <a id="1449" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="1453" class="Symbol">(</a><a id="1454" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1458" href="README.Data.Container.FreeMonad.html#1443" class="Bound">n</a><a id="1459" class="Symbol">)</a> <a id="1461" href="Effect.Monad.html#1007" class="Function Operator">&gt;&gt;</a>
  <a id="1466" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="1471" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a>
  <a id="1478" class="Keyword">where</a>
  <a id="1486" class="Keyword">open</a> <a id="1491" href="Effect.Monad.html#775" class="Module">RawMonad</a> <a id="1500" href="Data.Container.FreeMonad.html#4256" class="Function">monad</a> <a id="1506" class="Keyword">using</a> <a id="1512" class="Symbol">(</a><a id="1513" href="Effect.Monad.html#1007" class="Function Operator">_&gt;&gt;_</a><a id="1517" class="Symbol">)</a>

<a id="runState"></a><a id="1520" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="1529" class="Symbol">:</a> <a id="1531" class="Symbol">{</a><a id="1532" href="README.Data.Container.FreeMonad.html#1532" class="Bound">S</a> <a id="1534" href="README.Data.Container.FreeMonad.html#1534" class="Bound">X</a> <a id="1536" class="Symbol">:</a> <a id="1538" href="Agda.Primitive.html#320" class="Primitive">Set</a><a id="1541" class="Symbol">}</a> <a id="1543" class="Symbol">→</a> <a id="1545" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1551" href="README.Data.Container.FreeMonad.html#1532" class="Bound">S</a> <a id="1553" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1555" href="README.Data.Container.FreeMonad.html#1534" class="Bound">X</a> <a id="1557" class="Symbol">→</a> <a id="1559" class="Symbol">(</a><a id="1560" href="README.Data.Container.FreeMonad.html#1532" class="Bound">S</a> <a id="1562" class="Symbol">→</a> <a id="1564" href="README.Data.Container.FreeMonad.html#1534" class="Bound">X</a> <a id="1566" href="README.Data.Container.FreeMonad.html#606" class="Function Operator">⟨×⟩</a> <a id="1570" href="README.Data.Container.FreeMonad.html#1532" class="Bound">S</a><a id="1571" class="Symbol">)</a>
<a id="1573" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="1582" class="Symbol">(</a><a id="1583" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="1588" href="README.Data.Container.FreeMonad.html#1588" class="Bound">x</a><a id="1589" class="Symbol">)</a>                <a id="1606" class="Symbol">=</a> <a id="1608" class="Symbol">λ</a> <a id="1610" href="README.Data.Container.FreeMonad.html#1610" class="Bound">s</a> <a id="1612" class="Symbol">→</a> <a id="1614" href="README.Data.Container.FreeMonad.html#1588" class="Bound">x</a> <a id="1616" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1618" href="README.Data.Container.FreeMonad.html#1610" class="Bound">s</a>
<a id="1620" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="1629" class="Symbol">(</a><a id="1630" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1637" class="Symbol">((</a><a id="1639" href="Data.Sum.Base.html#675" class="InductiveConstructor">inj₁</a> <a id="1644" class="Symbol">_)</a> <a id="1647" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1649" href="README.Data.Container.FreeMonad.html#1649" class="Bound">k</a><a id="1650" class="Symbol">))</a> <a id="1653" class="Symbol">=</a> <a id="1655" class="Symbol">λ</a> <a id="1657" href="README.Data.Container.FreeMonad.html#1657" class="Bound">s</a> <a id="1659" class="Symbol">→</a> <a id="1661" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="1670" class="Symbol">(</a><a id="1671" href="README.Data.Container.FreeMonad.html#1649" class="Bound">k</a> <a id="1673" href="README.Data.Container.FreeMonad.html#1657" class="Bound">s</a><a id="1674" class="Symbol">)</a> <a id="1676" href="README.Data.Container.FreeMonad.html#1657" class="Bound">s</a>
<a id="1678" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="1687" class="Symbol">(</a><a id="1688" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1695" class="Symbol">((</a><a id="1697" href="Data.Sum.Base.html#700" class="InductiveConstructor">inj₂</a> <a id="1702" href="README.Data.Container.FreeMonad.html#1702" class="Bound">s</a><a id="1703" class="Symbol">)</a> <a id="1705" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1707" href="README.Data.Container.FreeMonad.html#1707" class="Bound">k</a><a id="1708" class="Symbol">))</a> <a id="1711" class="Symbol">=</a> <a id="1713" class="Symbol">λ</a> <a id="1715" href="README.Data.Container.FreeMonad.html#1715" class="Bound">_</a> <a id="1717" class="Symbol">→</a> <a id="1719" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="1728" class="Symbol">(</a><a id="1729" href="README.Data.Container.FreeMonad.html#1707" class="Bound">k</a> <a id="1731" class="Symbol">_)</a> <a id="1734" href="README.Data.Container.FreeMonad.html#1702" class="Bound">s</a>

<a id="test"></a><a id="1737" href="README.Data.Container.FreeMonad.html#1737" class="Function">test</a> <a id="1742" class="Symbol">:</a> <a id="1744" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="1753" href="README.Data.Container.FreeMonad.html#1394" class="Function">prog</a> <a id="1758" class="Number">0</a> <a id="1760" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1762" class="Symbol">(</a><a id="1763" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a> <a id="1768" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1770" class="Number">1</a><a id="1771" class="Symbol">)</a>
<a id="1773" href="README.Data.Container.FreeMonad.html#1737" class="Function">test</a> <a id="1778" class="Symbol">=</a> <a id="1780" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">P.refl</a>

<a id="1788" class="Comment">-- It should be noted that @State S ⋆ X@ is not the state monad. If we</a>
<a id="1859" class="Comment">-- could quotient @State S ⋆ X@ by the seven axioms of state (see</a>
<a id="1925" class="Comment">-- Plotkin and Power&#39;s &quot;Notions of Computation Determine Monads&quot;, 2002)</a>
<a id="1997" class="Comment">-- then we would get the state monad.</a>

<a id="2036" class="Comment">------------------------------------------------------------------------</a>
<a id="2109" class="Comment">-- Defining effectful inductive data structures</a>

<a id="2158" class="Comment">-- The definition of `C ⋆ A` is strictly positive in `A`, meaning that we</a>
<a id="2232" class="Comment">-- can use `C ⋆_` when defining (co)inductive datatypes.</a>


<a id="2291" class="Keyword">open</a> <a id="2296" class="Keyword">import</a> <a id="2303" href="Size.html" class="Module">Size</a>
<a id="2308" class="Keyword">open</a> <a id="2313" class="Keyword">import</a> <a id="2320" href="Codata.Sized.Thunk.html" class="Module">Codata.Sized.Thunk</a>
<a id="2339" class="Keyword">open</a> <a id="2344" class="Keyword">import</a> <a id="2351" href="Data.Vec.Base.html" class="Module">Data.Vec.Base</a> <a id="2365" class="Keyword">using</a> <a id="2371" class="Symbol">(</a><a id="2372" href="Data.Vec.Base.html#1076" class="Datatype">Vec</a><a id="2375" class="Symbol">;</a> <a id="2377" href="Data.Vec.Base.html#1112" class="InductiveConstructor">[]</a><a id="2379" class="Symbol">;</a> <a id="2381" href="Data.Vec.Base.html#1131" class="InductiveConstructor Operator">_∷_</a><a id="2384" class="Symbol">)</a>


<a id="2388" class="Comment">-- A `Tap C A` is a infinite source of `A`s provided that we can perform</a>
<a id="2461" class="Comment">-- the effectful computations described by `C`.</a>
<a id="2509" class="Comment">-- The first one can be accessed readily but the rest of them is hidden</a>
<a id="2581" class="Comment">-- under layers of `C` computations.</a>

<a id="2619" class="Keyword">module</a> <a id="2626" href="README.Data.Container.FreeMonad.html#2626" class="Module">_</a> <a id="2628" class="Symbol">(</a><a id="2629" href="README.Data.Container.FreeMonad.html#2629" class="Bound">C</a> <a id="2631" class="Symbol">:</a> <a id="2633" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="2643" href="Level.html#521" class="Function">0ℓ</a> <a id="2646" href="Level.html#521" class="Function">0ℓ</a><a id="2648" class="Symbol">)</a> <a id="2650" class="Symbol">(</a><a id="2651" href="README.Data.Container.FreeMonad.html#2651" class="Bound">A</a> <a id="2653" class="Symbol">:</a> <a id="2655" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2659" href="Level.html#521" class="Function">0ℓ</a><a id="2661" class="Symbol">)</a> <a id="2663" class="Keyword">where</a>

  <a id="2672" class="Keyword">data</a> <a id="2677" href="README.Data.Container.FreeMonad.html#2677" class="Datatype">Tap</a> <a id="2681" class="Symbol">(</a><a id="2682" href="README.Data.Container.FreeMonad.html#2682" class="Bound">i</a> <a id="2684" class="Symbol">:</a> <a id="2686" href="Agda.Builtin.Size.html#196" class="Postulate">Size</a><a id="2690" class="Symbol">)</a> <a id="2692" class="Symbol">:</a> <a id="2694" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2698" href="Level.html#521" class="Function">0ℓ</a> <a id="2701" class="Keyword">where</a>
    <a id="2711" href="README.Data.Container.FreeMonad.html#2711" class="InductiveConstructor Operator">_∷_</a> <a id="2715" class="Symbol">:</a> <a id="2717" href="README.Data.Container.FreeMonad.html#2651" class="Bound">A</a> <a id="2719" class="Symbol">→</a> <a id="2721" href="Codata.Sized.Thunk.html#482" class="Record">Thunk</a> <a id="2727" class="Symbol">(λ</a> <a id="2730" href="README.Data.Container.FreeMonad.html#2730" class="Bound">i</a> <a id="2732" class="Symbol">→</a> <a id="2734" href="README.Data.Container.FreeMonad.html#2629" class="Bound">C</a> <a id="2736" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2738" href="README.Data.Container.FreeMonad.html#2677" class="Datatype">Tap</a> <a id="2742" href="README.Data.Container.FreeMonad.html#2730" class="Bound">i</a><a id="2743" class="Symbol">)</a> <a id="2745" href="README.Data.Container.FreeMonad.html#2682" class="Bound">i</a> <a id="2747" class="Symbol">→</a> <a id="2749" href="README.Data.Container.FreeMonad.html#2677" class="Datatype">Tap</a> <a id="2753" href="README.Data.Container.FreeMonad.html#2682" class="Bound">i</a>

<a id="2756" class="Comment">-- We can run a given tap for a set number of steps and collect the elements</a>
<a id="2833" class="Comment">-- thus generated along the way. This gives us a `C ⋆_` computation of a vector.</a>

<a id="2915" class="Keyword">module</a> <a id="2922" href="README.Data.Container.FreeMonad.html#2922" class="Module">_</a> <a id="2924" class="Symbol">{</a><a id="2925" href="README.Data.Container.FreeMonad.html#2925" class="Bound">C</a> <a id="2927" class="Symbol">:</a> <a id="2929" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="2939" href="Level.html#521" class="Function">0ℓ</a> <a id="2942" href="Level.html#521" class="Function">0ℓ</a><a id="2944" class="Symbol">}</a> <a id="2946" class="Symbol">{</a><a id="2947" href="README.Data.Container.FreeMonad.html#2947" class="Bound">A</a> <a id="2949" class="Symbol">:</a> <a id="2951" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2955" href="Level.html#521" class="Function">0ℓ</a><a id="2957" class="Symbol">}</a> <a id="2959" class="Keyword">where</a>

  <a id="2968" href="README.Data.Container.FreeMonad.html#2968" class="Function">take</a> <a id="2973" class="Symbol">:</a> <a id="2975" href="README.Data.Container.FreeMonad.html#2677" class="Datatype">Tap</a> <a id="2979" href="README.Data.Container.FreeMonad.html#2925" class="Bound">C</a> <a id="2981" href="README.Data.Container.FreeMonad.html#2947" class="Bound">A</a> <a id="2983" class="Symbol">_</a> <a id="2985" class="Symbol">→</a> <a id="2987" class="Symbol">(</a><a id="2988" href="README.Data.Container.FreeMonad.html#2988" class="Bound">n</a> <a id="2990" class="Symbol">:</a> <a id="2992" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2993" class="Symbol">)</a> <a id="2995" class="Symbol">→</a> <a id="2997" href="README.Data.Container.FreeMonad.html#2925" class="Bound">C</a> <a id="2999" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3001" href="Data.Vec.Base.html#1076" class="Datatype">Vec</a> <a id="3005" href="README.Data.Container.FreeMonad.html#2947" class="Bound">A</a> <a id="3007" href="README.Data.Container.FreeMonad.html#2988" class="Bound">n</a>
  <a id="3011" href="README.Data.Container.FreeMonad.html#2968" class="Function">take</a> <a id="3016" class="Symbol">_</a> <a id="3018" class="Number">0</a> <a id="3020" class="Symbol">=</a> <a id="3022" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3027" href="Data.Vec.Base.html#1112" class="InductiveConstructor">[]</a>
  <a id="3032" href="README.Data.Container.FreeMonad.html#2968" class="Function">take</a> <a id="3037" class="Symbol">(</a><a id="3038" href="README.Data.Container.FreeMonad.html#3038" class="Bound">x</a> <a id="3040" href="README.Data.Container.FreeMonad.html#2711" class="InductiveConstructor Operator">∷</a> <a id="3042" class="Symbol">_)</a> <a id="3045" class="Number">1</a> <a id="3047" class="Symbol">=</a> <a id="3049" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3054" class="Symbol">(</a><a id="3055" href="README.Data.Container.FreeMonad.html#3038" class="Bound">x</a> <a id="3057" href="Data.Vec.Base.html#1131" class="InductiveConstructor Operator">∷</a> <a id="3059" href="Data.Vec.Base.html#1112" class="InductiveConstructor">[]</a><a id="3061" class="Symbol">)</a>
  <a id="3065" href="README.Data.Container.FreeMonad.html#2968" class="CatchallClause Function">take</a><a id="3069" class="CatchallClause"> </a><a id="3070" class="CatchallClause Symbol">(</a><a id="3071" href="README.Data.Container.FreeMonad.html#3071" class="CatchallClause Bound">x</a><a id="3072" class="CatchallClause"> </a><a id="3073" href="README.Data.Container.FreeMonad.html#2711" class="CatchallClause InductiveConstructor Operator">∷</a><a id="3074" class="CatchallClause"> </a><a id="3075" href="README.Data.Container.FreeMonad.html#3075" class="CatchallClause Bound">mxs</a><a id="3078" class="CatchallClause Symbol">)</a><a id="3079" class="CatchallClause"> </a><a id="3080" class="CatchallClause Symbol">(</a><a id="3081" href="Agda.Builtin.Nat.html#217" class="CatchallClause InductiveConstructor">suc</a><a id="3084" class="CatchallClause"> </a><a id="3085" href="README.Data.Container.FreeMonad.html#3085" class="CatchallClause Bound">n</a><a id="3086" class="CatchallClause Symbol">)</a> <a id="3088" class="Symbol">=</a> <a id="3090" class="Keyword">do</a>
    <a id="3097" href="README.Data.Container.FreeMonad.html#3097" class="Bound">xs</a> <a id="3100" href="Data.Container.FreeMonad.html#3830" class="Function Operator">←</a> <a id="3102" href="README.Data.Container.FreeMonad.html#3075" class="Bound">mxs</a> <a id="3106" class="Symbol">.</a><a id="3107" href="Codata.Sized.Thunk.html#556" class="Field">force</a>
    <a id="3117" href="README.Data.Container.FreeMonad.html#3117" class="Bound">rest</a> <a id="3122" href="Data.Container.FreeMonad.html#3830" class="Function Operator">←</a> <a id="3124" href="README.Data.Container.FreeMonad.html#2968" class="Function">take</a> <a id="3129" href="README.Data.Container.FreeMonad.html#3097" class="Bound">xs</a> <a id="3132" href="README.Data.Container.FreeMonad.html#3085" class="Bound">n</a>
    <a id="3138" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3143" class="Symbol">(</a><a id="3144" href="README.Data.Container.FreeMonad.html#3071" class="Bound">x</a> <a id="3146" href="Data.Vec.Base.html#1131" class="InductiveConstructor Operator">∷</a> <a id="3148" href="README.Data.Container.FreeMonad.html#3117" class="Bound">rest</a><a id="3152" class="Symbol">)</a>

<a id="3155" class="Comment">-- A stream of all the natural numbers starting from a given value is an</a>
<a id="3228" class="Comment">-- example of a tap.</a>

<a id="natsFrom"></a><a id="3250" href="README.Data.Container.FreeMonad.html#3250" class="Function">natsFrom</a> <a id="3259" class="Symbol">:</a> <a id="3261" class="Symbol">∀</a> <a id="3263" class="Symbol">{</a><a id="3264" href="README.Data.Container.FreeMonad.html#3264" class="Bound">i</a><a id="3265" class="Symbol">}</a> <a id="3267" class="Symbol">→</a> <a id="3269" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="3275" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="3277" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3279" href="README.Data.Container.FreeMonad.html#2677" class="Datatype">Tap</a> <a id="3283" class="Symbol">(</a><a id="3284" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="3290" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="3291" class="Symbol">)</a> <a id="3293" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="3295" href="README.Data.Container.FreeMonad.html#3264" class="Bound">i</a>
<a id="3297" href="README.Data.Container.FreeMonad.html#3250" class="Function">natsFrom</a> <a id="3306" class="Symbol">=</a> <a id="3308" class="Keyword">let</a> <a id="3312" class="Keyword">open</a> <a id="3317" href="Effect.Monad.html#775" class="Module">RawMonad</a> <a id="3326" href="Data.Container.FreeMonad.html#4256" class="Function">monad</a> <a id="3332" class="Keyword">using</a> <a id="3338" class="Symbol">(</a><a id="3339" href="Effect.Monad.html#1007" class="Function Operator">_&gt;&gt;_</a><a id="3343" class="Symbol">)</a> <a id="3345" class="Keyword">in</a> <a id="3348" class="Keyword">do</a>
  <a id="3353" href="README.Data.Container.FreeMonad.html#3353" class="Bound">n</a> <a id="3355" href="Data.Container.FreeMonad.html#3830" class="Function Operator">←</a> <a id="3357" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a>
  <a id="3363" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="3367" class="Symbol">(</a><a id="3368" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="3372" href="README.Data.Container.FreeMonad.html#3353" class="Bound">n</a><a id="3373" class="Symbol">)</a>
  <a id="3377" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3382" class="Symbol">(</a><a id="3383" href="README.Data.Container.FreeMonad.html#3353" class="Bound">n</a> <a id="3385" href="README.Data.Container.FreeMonad.html#2711" class="InductiveConstructor Operator">∷</a> <a id="3387" class="Symbol">λ</a> <a id="3389" class="Keyword">where</a> <a id="3395" class="Symbol">.</a><a id="3396" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="3402" class="Symbol">→</a> <a id="3404" href="README.Data.Container.FreeMonad.html#3250" class="Function">natsFrom</a><a id="3412" class="Symbol">)</a>

<a id="3415" class="Comment">-- We can use `take` to capture an initial segment of the `natsFrom` tap</a>
<a id="3488" class="Comment">-- and, after running the state operations, observe that it does generate</a>
<a id="3562" class="Comment">-- successive numbers.</a>

<a id="3586" href="README.Data.Container.FreeMonad.html#3586" class="Function">_</a> <a id="3588" class="Symbol">:</a> <a id="3590" class="Symbol">∀</a> <a id="3592" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a> <a id="3594" class="Symbol">→</a>
    <a id="3600" href="README.Data.Container.FreeMonad.html#1520" class="Function">runState</a> <a id="3609" class="Symbol">(</a><a id="3610" href="README.Data.Container.FreeMonad.html#3250" class="Function">natsFrom</a> <a id="3619" href="Data.Container.FreeMonad.html#3830" class="Function Operator">&gt;&gt;=</a> <a id="3623" class="Symbol">λ</a> <a id="3625" href="README.Data.Container.FreeMonad.html#3625" class="Bound">ns</a> <a id="3628" class="Symbol">→</a> <a id="3630" href="README.Data.Container.FreeMonad.html#2968" class="Function">take</a> <a id="3635" href="README.Data.Container.FreeMonad.html#3625" class="Bound">ns</a> <a id="3638" class="Number">5</a><a id="3639" class="Symbol">)</a> <a id="3641" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a>
  <a id="3645" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3647" class="Symbol">(</a><a id="3648" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a> <a id="3650" class="InductiveConstructor Operator">∷</a> <a id="3652" class="Number">1</a> <a id="3654" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3656" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a> <a id="3658" class="InductiveConstructor Operator">∷</a> <a id="3660" class="Number">2</a> <a id="3662" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3664" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a> <a id="3666" class="InductiveConstructor Operator">∷</a> <a id="3668" class="Number">3</a> <a id="3670" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3672" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a> <a id="3674" class="InductiveConstructor Operator">∷</a> <a id="3676" class="Number">4</a> <a id="3678" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3680" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a> <a id="3682" class="InductiveConstructor Operator">∷</a> <a id="3684" href="Data.Vec.Base.html#1112" class="InductiveConstructor">[]</a> <a id="3687" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3689" class="Number">5</a> <a id="3691" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3693" href="README.Data.Container.FreeMonad.html#3592" class="Bound">k</a><a id="3694" class="Symbol">)</a>
<a id="3696" class="Symbol">_</a> <a id="3698" class="Symbol">=</a> <a id="3700" class="Symbol">λ</a> <a id="3702" href="README.Data.Container.FreeMonad.html#3702" class="Bound">k</a> <a id="3704" class="Symbol">→</a> <a id="3706" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
</pre></body></html>